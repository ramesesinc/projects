import com.rameses.common.*;
import com.rameses.annotations.*;
import com.rameses.services.extended.*;
import java.rmi.server.UID;

class LoanSpecialBillingAccountService extends ActiveCrudListService
{
	@Env
	def env;

	@ActiveDB('specialbillingaccount')
	def em;

	@ActiveDB('datatxnlog')
	def txnlog; 

	@Service('DateService')
	def dateSvc;

	@Service('InboxService')
	def inboxSvc;


	@ProxyMethod
	public def getColumns( params ) {
		def cols = [];

		if (!params.state) cols << [name: 'txnstate', caption: 'Status'];

		cols << [name: 'dtcreated', caption: 'Date Created'];
		cols << [name: 'author.name', caption: 'Created By'];
		cols << [name: 'borrower.name', caption: 'Borrower'];
		cols << [name: 'loanapp.appno', caption: 'App. No.'];
		cols << [name: 'loanapp.amount', caption: 'Loan Amount', type: 'decimal', format: '#,##0.00'];
		cols << [name: 'ledger.dtreleased', caption: 'Release Date', type: 'date', outputFormat: 'MMM-dd-yyyy'];
		cols << [name: 'ledger.dtmatured', caption: 'Maturity Date', type: 'date', outputFormat: 'MMM-dd-yyyy'];

		if (params.state == 'APPROVED') {
			cols << [name: 'dtposted', caption: 'Date Approved'];
			cols << [name: 'poster.name', caption: 'Approved By'];

		} else if (params.state == 'DISAPPROVED') {
			cols << [name: 'dtposted', caption: 'Date Disapproved'];
			cols << [name: 'poster.name', caption: 'Disapproved By'];

		} else if (params.state == 'CLOSED') {
			cols << [name: 'dtclosed', caption: 'Date Closed'];
			cols << [name: 'closedby.name', caption: 'Closed By'];

		}

		return cols;
	}

	void beforeList( params ) {
		params.searchtext = (params.searchtext? params.searchtext : '') + '%'
		if (params.state) {
			params._listMethod = 'getListByState';
		}
	}
	
	@ProxyMethod
	public def getStates() {
		return [
			[caption: 'All'],
			[caption: 'DRAFT', state: 'DRAFT'],
			[caption: 'FOR APPROVAL', state: 'FOR_APPROVAL'],
			[caption: 'APPROVED', state: 'APPROVED'],
			[caption: 'DISAPPROVED', state: 'DISAPPROVED'],
			[caption: 'CLOSED', state: 'CLOSED']
		];
	}

	private void validate( data ) {
		def i = em.findByLedgeridAndState([ledgerid: data.ledger?.objid, state: 'APPROVED']);
		if (i) throw new Exception('There already has an active special billing account for ' + i.borrower.name + ' with App. No. ' + i.loanapp.appno + '.');
	}

	void beforeCreate( data ) {
		validate(data);
		data.dtcreated =dateSvc.serverDate;
		data.author = [objid: env.USERID, name: env.NAME];
		data.txndate = data.dtcreated;
		if (!data.txnstate) data.txnstate = 'DRAFT';
	}

	void beforeUpdate( data ) {
		validate(data);
	}

	@ProxyMethod
	public def submitForApproval( params ) {
		def data = changeState(params, 'FOR_APPROVAL');

		def msginfo = [ 
			objid 		: data.objid, 
			dtcreated	: dateSvc.serverDate, 
			refid 		: data.objid, 
			filetype 	: 'specialbillingaccount', 
			message 	: 'For approval special billing account ' + data.borrower.name + ' with App. No. ' + data.loanapp.appno, 
			senderid	: env.USERID, 
			sendername 	: env.NAME, 
			recipientid : 'APPROVER' 
		]; 
		inboxSvc.addMessage( msginfo );

		return open(data);
	}

	private def changeState( params, state ) {
		def data = em.read(params);
		if (!data) throw new Exception('Special billing account record does not exist or has already been deleted.');

		if (data.txnstate == state) {
			throw new Exception('Special billing account record has already been ' + state + '.');
		}

		data.txnstate = state;
		em.changeState(data);

		def action, remarks;
		switch (state.toUpperCase()) {
			case 'FOR_APPROVAL'	: 	action = 'For Approval';
									remarks = 'Special billing account submitted for approval.';
									break;
			case 'APPROVED' 	: 	action = 'APPROVED';
									remarks = 'Special billing account approved.';
									break;
			case 'DISAPPROVED' 	: 	action = 'DISAPPROVED';
									remarks = 'Special billing account disapproved.';
									break;
			case 'CLOSED' 		: 	action = 'CLOSED';
									remarks = 'Special billing account closed.';
									break;
		}

		if (action && remarks) {
			def loginfo = [
				objid 	: 'TLOG' + new UID(), 
				txndate : dateSvc.serverDate, 
				txnid   : data.objid, 
				user    : [objid: env.USERID, username: env.USER], 
				action	: action, 
				remarks : remarks
			]; 
			txnlog.create( loginfo );	
		}

		return open(data);
	}

	@ProxyMethod
	public def approveDocument( params ) {
		return approveImpl(params, 'APPROVED');
	}

	@ProxyMethod
	public def disapprove( params ) {
		return approveImpl(params, 'DISAPPROVED');
	}

	private def approveImpl( params, state ) {
		def data = changeState(params, state);

		data.dtposted = dateSvc.serverDate;
		data.poster = [objid: env.USERID, name: env.NAME];
		em.update(data, 'post')

		inboxSvc.removeMessageByRefid([refid: data.objid]);

		return open(data);
	}


}