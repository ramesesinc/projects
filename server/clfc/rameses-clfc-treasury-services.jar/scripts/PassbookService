import com.rameses.annotations.*;
import com.rameses.common.*;
import com.rameses.services.extended.*;
import java.rmi.server.UID;

class PassbookService extends ActiveCrudListService 
{	
	@ActiveDB('passbook')
	def em; 

	@ActiveDB('bank')
	def bank;

	@ActiveDB('datatxnlog')
	def txnlog; 	

	@Service('DateService')
	def dateSvc;

	@Env 
	def env;
	
	@ProxyMethod
	public def getColumns( params ) {
		return [
			[name:"dtcreated", caption:"Date Created"], 
			[name:"bank.objid", caption:"Bank"], 
			[name:"acctno", caption:"Account No."], 
			[name:"acctname", caption:"Account Name"], 
			[name:"passbookno", caption:"Passbook No."], 
			[name:"txnstate", caption:"Status"]
		];
	}

	void afterOpen( data ) {
		data.bank = bank.read(data.bank);
	}

	void beforeCreate( data ) { 
		if (!data.txnstate) data.txnstate = 'ACTIVE';

		data.dtcreated = dateSvc.serverDate.toString(); 
		data.author = [objid: env.USERID, username: env.USER];  		
	} 

	void afterCreate( data ) {
		def loginfo = [
			objid 	: 'TLOG' + new UID(), 
			txndate : data.dtcreated, 
			txnid   : data.objid, 
			user    : data.author, 
			action	: 'CREATE', 
			remarks : 'New Passbook '+ data.passbookno  
		]; 
		txnlog.create( loginfo ); 
	}

	void afterUpdate( data ) {
		def loginfo = [
			objid 	: 'TLOG' + new UID(), 
			txndate : dateSvc.serverDate, 
			txnid   : data.objid, 
			user    : [objid: env.USERID, username: env.USER], 
			action	: 'UPDATE', 
			remarks : 'Updated Passbook '+ data.passbookno   
		]; 
		txnlog.create( loginfo ); 
	}

	void afterRemoveEntity( data ) {
		if (!data.remarks) data.remarks = 'Deleted passbook record'; 

		def loginfo = [
			objid 	: 'TLOG' + new UID(), 
			txndate : dateSvc.serverDate, 
			txnid   : data.objid, 
			user    : [objid: env.USERID, username: env.USER], 
			action	: 'DELETE', 
			remarks : data.remarks 
		]; 
		txnlog.create( loginfo ); 
	}	

	void beforeList( params ) {
		params.searchtext = (params.searchtext? params.searchtext: '')+'%'; 
	}


	@ProxyMethod 
	public def activate( params ) {
		params.txnstate = 'ACTIVE';
		return changeState( params ); 
	} 

	@ProxyMethod 
	public def deactivate( params ) {
		params.txnstate = 'DEACTIVATED';
		return changeState( params ); 
	}	

	@ProxyMethod 
	public def closeAccount( params ) {
		if (!params.objid) throw new Exception('Please specify objid'); 
		if (!params.remarks) throw new Exception('Please specify remarks'); 

		params.txnstate = 'CLOSED';
		return changeState( params ); 
	}	

	private def changeState( params ) {
		if (!params.objid) throw new Exception('Please specify objid'); 
		if (!params.txnstate) throw new Exception('Please specify txnstate'); 

		def data = em.read(params); 
		if (!data) throw new Exception("'"+params.objid+"' bank record not found"); 

		data.txnstate = params.txnstate;
		em.update(data);

		def loginfo = [
			objid 	: 'TLOG' + new UID(), 
			txndate : dateSvc.serverDate, 
			txnid   : data.objid, 
			user    : [objid: env.USERID, username: env.USER], 
			action	: 'CHANGE_STATE', 
			remarks : data.txnstate  
		]; 
		if (data.txnstate == 'CLOSED') {
			loginfo.action = 'CLOSE'; 
			loginfo.remarks = params.remarks; 
		} 
		txnlog.create( loginfo ); 
		return data; 
	} 
} 