import com.rameses.annotations.*;
import com.rameses.util.*;
import java.rmi.server.*;


/**********************************************************
* This routine is hooked in the CashReceiptService and 
* CashReceiptVoidService services. It will update each
* receivable based on amount paid. It will update billing.
* Billing is the key to determine if license can be
* released.     
**********************************************************/
public class BusinessCashReceiptInterceptor {

	@Service("BusinessPaymentService")
	def pmtSvc;

	@Service("BusinessBillingService")
	def billingSvc;

	@Service("BusinessApplicationService")
	def appSvc;

	@Service("BusinessApplicationWorkflowService")
	def workflowSvc;

	@ActiveDB("business_payment")
	def business_payment_db;

	@After(pattern="CashReceiptService.post", eval="#{result.collectiontype?.handler == 'bpls'}")
	public void postBusinessPayment(def evt) {
		def a = evt.args[0];
		if (a.taxfees.findAll{ !it.receivableid }) 
			throw new Exception("each item must have receivableid"); 

		//recheck the application status
		def appstate = appSvc.getStatus([objid: a.applicationid]); 
		if (!appstate.toString().toLowerCase().matches('payment|release|completed')) 
			throw new Exception("The current application status is not allowed for this transaction. Please verify."); 

		def r = evt.result;
		def m = [:];
		m.objid = r.objid;
		m.businessid = a.businessid;
		m.applicationid=a.applicationid;
		m.appyear = a.appyear;
		m.refid = r.objid;
		m.reftype = 'cashreceipt';
		m.refno = a.receiptno;
		m.refdate = a.receiptdate;
		m.amount = a.amount;
		m.remarks = a.remarks;
		m.voided = 0;
		m.taxcredit = a.totalcredit;
		if (m.taxcredit == null) m.taxcredit = 0;
		m.paymentmode = a.paymentmode;		
		m.items = [];
		a.taxfees.each {tf-> 
			if ( tf.details ) {
				tf.details.each{d-> 
					if ( d.partial == null) d.partial=0; 

					m.items << d; 
				}
			} else {
				if ( tf.partial == null) tf.partial=0; 
				if ( tf.qtr == null ) tf.qtr=0;
				m.items << tf; 
			} 
		}
		pmtSvc.create( m );

		def app = appSvc.open([ objid: m.applicationid ]);
		if (app.state == 'COMPLETED') return;

		def stat = appSvc.checkReleaseStatus([ 
			objid: m.applicationid, apptype: app.apptype, 
			txnmode: app.txnmode, appyear: app.appyear, receiptdate: a.receiptdate 
		]); 

		if ( stat.canRelease == true ) { 
			def tsk = workflowSvc.getTaskFromAppid([ applicationid: m.applicationid ]); 
			if ( tsk?.state == 'payment' ) { 
				tsk.action = 'release'; 
				tsk.data = [objid: m.applicationid, appno: app.appno, business: app.business]; 
				workflowSvc.signal( tsk ); 
			} 
		} 
	} 

	@After(pattern="CashReceiptVoidService.post",index=0,eval="#{result.receipt.collectiontype.handler=='bpls'}")
	public void voidTxn( evt ) {
		//check if permit is already issued.
		def r = evt.args[0].receipt; 
		if ( !business_payment_db.read([ objid: r.objid ])?.objid ) { 
			// record not found exit immediately 
			return; 
		} 

		def pmt = pmtSvc.open([ objid: r.objid ]);
		if ( !pmt?.objid ) throw new Exception("Business payment not found");

		//check if app is completed.
		def app = appSvc.open([objid: pmt.applicationid]);
		def result = evt.result; 
		pmtSvc.voidPayment( [objid: r.objid, voidrct: result ] );

		if (app.state != 'COMPLETED') {
			def stat = appSvc.checkReleaseStatus([ 
				objid: app.objid, apptype: app.apptype, 
				txnmode: 'ONLINE', appyear: app.appyear, receiptdate: r.receiptdate 
			]); 
			if ( stat.canRelease ) return; 

			def tsk = workflowSvc.getTaskFromAppid([applicationid: pmt.applicationid]);
			if ( tsk?.state == 'release' ) {
				tsk.action = 'revert';
				tsk.data = [objid: pmt.applicationid, appno: app.appno, business: app.business];
				workflowSvc.signal( tsk ); 
			}
		}
	}
}