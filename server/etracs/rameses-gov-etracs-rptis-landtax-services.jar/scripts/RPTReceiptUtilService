import com.rameses.annotations.*
import com.rameses.common.*
import com.rameses.services.extended.*

class RPTReceiptUtilService
{
	@Service('DateService')
	def dtSvc 

	@ActiveDB('rptreceipt_utility')
	def em

	@DataContext('cashreceiptitem')
	def em_cashreceiptitem

	@DataContext('cashreceiptrpt_item_account')
	def em_cashreceiptrpt_item_account


	@ProxyMethod
	public void postShares( params ) {
		if (!params.year) throw new Exception('year must be specified.')
		if (!params.month) throw new Exception('mon must be specified.')

		def remdate = params.year + '-' + params.month.index.toString().padLeft(2,'0') + '-01'
		params.remfromdate = dtSvc.parse('yyyy-MM-dd', remdate)
		params.remtodate = dtSvc.getMonthEndDate(params.remfromdate) + 1

		def unpostedlist = em.getReceiptsWithUnpostedShares(params)
		if (!unpostedlist) throw new Exception('There are no unposted shares for this period.')

		unpostedlist.each{rct ->
			em_cashreceiptitem.find([receiptid:rct.objid]).list().each{item ->
				def share = createShare(rct, item)
				if (share){
					em_cashreceiptrpt_item_account.save(share)
				}
			}
		}
	}


	def createShare(rct, item){
		def share = em.findShareAccount([itemid:item.item.objid])
		if (!share) return null 
		share.putAll(item) 
		share.rptledgerid = rct.rptledgerid
		share.rptreceiptid = rct.objid 
		share.discount = 0.0 
		return share 
	}
}
