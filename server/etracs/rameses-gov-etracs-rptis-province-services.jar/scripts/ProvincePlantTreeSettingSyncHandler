import com.rameses.osiris3.xconnection.*;
import com.rameses.annotations.*;
import com.rameses.common.*;

class ProvincePlantTreeSettingSyncHandler
{   
	@DataContext('rysetting_planttree')
	def em_planttree 

	@DataContext('rysetting_lgu')
	def em_setting_lgu

	@Env
	def env 

	@Context 
	def appCtx;

	@Service()
	def self

	@Service('PlantTreeRYSettingService')
	def svc  

	@Service('OrgService')
	def orgSvc 



	@ProxyMethod 
	public def syncData( params ) {
		self.doSync(params)
		return new AsyncToken()
	}


	@Async 
	@ProxyMethod
	public void doSync( params ) {
		def xconn = appCtx.getResource( XConnection.class, env.ASYNC.CONNECTION ); 
		def queue = xconn.register( env.ASYNC.ID ); 

		// params : [orgcode:'xxx', ry:xxxx ]
		//get lguid
		def org = orgSvc.findByCode([code:params.orgcode])
		if (! org){
			queue.push([msgtype:'_ERROR_', msg:'Organization code not found.'])
			queue.push( new AsyncToken(closed:true) )
			return
		}
		

		def settings = []
		def lgusettings = em_setting_lgu.find([lguid:org.objid, settingtype:'planttree']).list()
		lgusettings.each{
			settings << em_planttree.find([objid:it.rysettingid, ry:params.ry]).first()
		}
		if (!settings){
			queue.push([msgtype:'_ERROR_', msg:'Requested revision setting does not exist.'])
			queue.push( new AsyncToken(closed:true) )
			return
		}

		settings.each{setting ->
			setting = svc.open(setting)
			queue.push([msgtype:'planttreerysetting', data:setting])

			def assesslevels = svc.getAssessLevels(setting)
			queue.push([msgtype:'assesslevels', data:assesslevels])

			def unitvalues = svc.getUnitValues(setting, null)
			queue.push([msgtype:'planttreeunitvalues', data:unitvalues])

		}

		queue.push( new AsyncToken(closed:true) )
	}
}