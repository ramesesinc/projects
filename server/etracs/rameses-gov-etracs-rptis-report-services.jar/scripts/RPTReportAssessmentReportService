import com.rameses.annotations.*
import com.rameses.common.*;
import com.rameses.services.extended.*;

class RPTReportAssessmentReportService
{
	@Env 
	def env

	@ActiveDB('rptreport_assessment_report')
	def em

	@Service('NumberService')
	def numSvc 

	@DataContext('barangay')
	def em_barangay

	@Service('RPTUtil')
	def util 


	@Async
    @ProxyMethod
	public def buildReport(params){
		params.monthid = params.month?.index
		params.lguid = params.lgu.objid

		util.buildStartEndDateFilter(params)

		if ('MV'.equalsIgnoreCase(params.valuetype)){
			params.valuefield = 'r.totalmv'
			params.valuecaption = 'MARKET VALUE'
		}
		else {
			params.valuefield = 'r.totalav'
			params.valuecaption = 'ASSESSED VALUE'
		}

		def brgys = []
		if (params.reportformat.type == 'standard'){
			brgys = buildStandardFormat(params)
		}
		else if (params.reportformat.type == 'lift'){
			brgys = buildLintFormat(params)	
		}
		else {
			throw new Exception('Format ' + params.reportformat.caption + ' is not yet supported.')
		}
		
		params.TITLE = 'MONTHLY ASSESSMENT REPORT'
		params.PERIOD = 'MONTH OF ' + params.month.caption + ', ' + params.year 
		params.FORLGUNAME = env.ORGCLASS +  ' OF ' + env.ORGNAME 
		if (params.lgu){
			params.FORLGUNAME = params.lgu.lgutype.toUpperCase() + ' OF ' + params.lgu.name
		}

		return [reportdata:brgys, parameters:params]
	}


	void updateBrgyInfo(brgys, list){
		list.each{v ->
			def brgy = brgys.find{it.objid == v.objid }
			if (brgy){
				brgy.putAll(v)
			}
		}
	}


	def buildStandardFormat(params){
		def brgys = getBarangays(params).each{it.barangay = it.name}
		updateBrgyInfo(brgys, em.getPreceedingList(params))
		updateBrgyInfo(brgys, em.getCurrentList(params))
		updateBrgyInfo(brgys, em.getCancelledList(params))
		updateBrgyInfo(brgys, em.getEndingList(params))
		setZeroValuesToNull(brgys)
		return brgys 
	}


	void updateLintBrgyInfo(result, list){
		list.each{v ->
			def res = result.find{it.objid == v.objid && it.rputypeidx == v.rputypeidx}
			if (res){
				res.putAll(v)
			}
		}
	}	

	def buildLintFormat(params){
		def result = []
		getBarangays(params).each{
			result << [objid:it.objid, barangay: it.name, rputypeidx:1, rputype:'land']
			result << [objid:it.objid, barangay: it.name, rputypeidx:2, rputype:'bldg']
			result << [objid:it.objid, barangay: it.name, rputypeidx:3, rputype:'mach']
			result << [objid:it.objid, barangay: it.name, rputypeidx:4, rputype:'other']
		}
		
		updateLintBrgyInfo(result, em.getPreceedingLintList(params))
		updateLintBrgyInfo(result, em.getCurrentLintList(params))
		updateLintBrgyInfo(result, em.getCancelledLintList(params))
		updateLintBrgyInfo(result, em.getEndingLintList(params))
		setZeroValuesToNull(result)
		return result 
	}


	void setZeroValuesToNull(brgys){
		brgys.each{brgy ->
			brgy.each{k, v->
				if (v == 0.0) brgy[k] = null 
			}
		}
	}

	def getBarangays(params){
		if ('city'.equalsIgnoreCase(env.ORGCLASS)){
			return em_barangay.select('objid,index,pin,name').where('1=1').orderBy('pin').list()
		}
		return em_barangay.select('objid,index,pin,name').find([parentid:params.lguid]).orderBy('pin').list()
	}

}

