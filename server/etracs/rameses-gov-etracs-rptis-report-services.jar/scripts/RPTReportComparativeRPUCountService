import com.rameses.annotations.*
import com.rameses.common.*;
import com.rameses.services.extended.*;
import java.rmi.server.UID

class RPTReportComparativeRPUCountService
{
    @ActiveDB('rptreport_comparativecount')
    def em

    @Service('Var')
    def var 

    @Service('PropertyClassificationService')
    def pcSvc 

    @Service('ExemptionTypeService')
    def etSvc

    @Service('RPTUtil')
    def util


    @Async
	@ProxyMethod
	public Map generateComparativeRpuCount(params) {
		params.lguid = params.lgu?.objid 
		if (params.barangay){
			params.barangayid = params.barangay.objid
		}

		util.buildReportFormalTitlePeriod('COMPARATIVE DATA ON NUMBER OF RPUs', params)

		util.buildPreceedingPeriodFilter(params)
		def taxablepreceedings 	= em.getPreceedingComparativeRpuCount(params)
		def exemptpreceedings 	= em.getPreceedingComparativeRpuCountExempt(params)

		util.buildCurrentPeriodFilter(params)
		def taxablecurrents 	= em.getNewDiscoveryComparativeRpuCount(params)
		def taxablecancelleds 	= em.getCancelledComparativeRpuCount(params)
		def exemptcurrents 	= em.getNewDiscoveryComparativeRpuCountExempt(params)
		def exemptcancelleds 	= em.getCancelledComparativeRpuCountExempt(params)
		
		util.buildEndingPeriodFilter(params)
		def taxableendings 		= em.getEndingComparativeRpuCount(params)
		def exemptendings 		= em.getEndingComparativeRpuCountExempt(params)
		
		def loadSumaryInfo = { data, list ->
			def info = list.find{ it.classid == data.classid }
			if( info ) data.putAll( info )
		}
	
		int lineno = 1
		int charindex = 'a' 
		def classifications = getClassifications()
		def firstspecial = classifications.find{ it.special == 1 }
		classifications.add( classifications.indexOf( firstspecial), [objid:'SPECIAL', name:'SPECIAL', special:0] )
		def taxables = classifications.collect{ buildComparativeRpuCountInfo(it, 'TAXABLE') }
		taxables.each { data ->
			loadSumaryInfo( data, taxablepreceedings )
			loadSumaryInfo( data, taxablecurrents )
			loadSumaryInfo( data, taxablecancelleds )
			loadSumaryInfo( data, taxableendings )
			if( data.special == 0 ) {
				data.classname = lineno + '. ' + data.classname 
				lineno += 1
			}
			else {
				data.classname = '   ' + ((char)charindex) + '. ' + data.classname 
				charindex += 1
			}
		}
		
		lineno = 1 
		def exempts  = getExemptionTypes().collect{ buildComparativeRpuCountInfo(it, 'EXEMPT') }
		exempts.each { data -> 
			loadSumaryInfo( data, exemptpreceedings )
			loadSumaryInfo( data, exemptcurrents )
			loadSumaryInfo( data, exemptcancelleds )
			loadSumaryInfo( data, exemptendings )
			data.classname = lineno + '. ' + data.classname 
			lineno += 1
		}

		def p = [
			PERIOD 		: params.period,
			PERIODTYPE 	: params.periodtype,
			TITLE 		: params.title,
			FORLGUNAME  : params.lgu.lgutype.toUpperCase() + ' OF ' + params.lgu.name,
		]
		
		return [
			parameters  : p, 
			reportdata  : taxables + exempts ,
		]
	}


	def buildComparativeRpuCountInfo( info, taxability ) {
		return [ classid	: info.objid, 
			classname	: info.name, 
			special		: info.special ,
			taxability	: taxability,
			preceedinglandcount 	: 0.0,
			preceedingimpcount 		: 0.0,
			preceedingtotal 		: 0.0,
			newdiscoverylandcount 	: 0.0,
			newdiscoveryimpcount 	: 0.0,
			newdiscoverytotal 		: 0.0,
			cancelledlandcount 		: 0.0,
			cancelledimpcount 		: 0.0,
			cancelledtotal 			: 0.0,
			endinglandcount 		: 0.0,
			endingimpcount 			: 0.0,
			endingtotal 			: 0.0,
		]
	}	


    @ProxyMethod
    public def getClassifications(){
    	return pcSvc.getClassifications([:])
    }

    
    @ProxyMethod
    public def getExemptionTypes(  ) {
    	return 	etSvc.getExemptionTypes([:])
    }


}
