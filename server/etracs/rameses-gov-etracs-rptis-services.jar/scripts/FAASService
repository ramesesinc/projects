import com.rameses.annotations.*
import com.rameses.common.*
import com.rameses.util.*
import com.rameses.services.extended.*


class FAASService
{
	@Env 
	def env
	
	@ActiveDB('faas')	
	def em 

	@ActiveDB('faas_list')	
	def faasListEm

	@Service('Var')
	def var 

	@Service('DateService')
	def dtSvc 

	@Service('RPUService')
	def rpuSvc

	@Service('RealPropertyService')
	def rpSvc 

	@Service('SequenceService')
	def seqSvc

	@Service('RPTUtil')
	def util

	@Service('TxnRefService')
	def txnRef

    @Service('FAASService')
    def faasSvc

    @Service('DBImageService')
    def imageSvc

    @Service('RPTTrackingService')
    def trackingSvc 

    @Service('NumberService')
    def numSvc 

    @Service('RPTRedFlagService')
    def redFlagSvc 

    @DataContext('realproperty')
    def realpropertyDb

    @DataContext('faas')
    def faasDb

    @DataContext('faas_list')
    def faaslistDb



    
    def SCHEMA_PREVIOUSFAAS		= 'previousfaas'
	def SCHEMA_BACKTAX			= 'backtax'
	def SCHEMA_SIGNATORY		= 'signatory'
	
	def STATE_INTERIM 				= 'INTERIM'
	def STATE_FORAPPROVAL 				= 'FORAPPROVAL'
	def STATE_CURRENT 				= 'CURRENT'
	def STATE_CANCELLED  			= 'CANCELLED'

	def UTDNO_KEY 				= 'UNIQUE_TDNO'


	@ProxyMethod
	public def initCaptureAndCreate( info ) {
		def faas = initCapture(info)
		faasSvc.createFaas(faas)
		return faas
	}

	@ProxyMethod
	public def initCapture( info ) {
		util.required('LGU', info.lgu)
		info.lguid 		 = info.lgu.objid 
		info.lgutype 	 = info.lgu.lgutype 
		
		def faas 		    = initFaas(info)
		faas.rp  			= rpSvc.initCapture(info)
		faas.realpropertyid = faas.rp.objid 
		faas.lgutype 	    = faas.rp.lgutype 
		faas.rpu 			= rpuSvc.initRpu(info)
		faas.rpuid  		= faas.rpu.objid
		faas.fullpin 		= faas.rpu.fullpin 
		faas.datacapture    = true
		faas.autonumber  	= ( info.autonumber != null ? info.autonumber : false)
		return faas
	}	


	@ProxyMethod
	public def initOnlineTransaction( info ) {
		def faas = faasSvc.initTransaction(info);
		createFaas(faas)
		insertSketch(info, faas)
		return faas
	}	


	@ProxyMethod
	public def initNewDiscovery( info ) {
		if (info.rputype != 'land'){
			checkLandTxnReference(info.rp)
			if (info.suffix == null ) 
				throw new Exception('Suffix is required.')

			def faas_new_discovery_validate_suffix = var.faas_new_discovery_validate_suffix
			if (faas_new_discovery_validate_suffix == null || util.isTrue(faas_new_discovery_validate_suffix)){
				def nextsuffix = rpuSvc.getNextSuffix([rputype:info.rputype, realpropertyid:info.rp.objid])
				if (nextsuffix != null &&  info.suffix < nextsuffix)
					throw new Exception('Suffix must be greater than or equal to ' + nextsuffix + '.')
			}

			rpuSvc.validateSuffix( info.rputype, info.suffix )
			info.fullpin = info.rp.pin + '-' + info.suffix 
		}
		def faas = initCapture(info)
		faas.txntype = getTxnType(faas.txntype.objid)
		faas.datacapture = false
		faas.prevtdno = 'NEW'
		faas.prevpin = ''
		if(info.state) faas.state = info.state 
        createFaas(faas)
        return faas
	}	


	@ProxyMethod
	public def initTransaction( info ) {
		def faas = openFaas(info.faas)
		faas._checkTxnRef = info._checkTxnRef
		validateFaasForTransaction(faas)
		initNewFaasInfo(info, faas)
		initNewRealPropertyInfo(info, faas)
		initNewRpuInfo(info, faas)
		return faas
	}	


	@ProxyMethod
	public def createFaas( faas ) {
		checkTxnReference(faas)
		if (!faas.ryordinanceno){
			faas.ryordinanceno = var.gr_ordinance_no
			faas.ryordinancedate = var.gr_ordinance_date
		}
		faas.originlguid = (faas.originlguid ? faas.originlguid : env.ORGID )
		generateUniqueTdNo(faas)
		updateNameInfo(faas)
		createRealProperty(faas)
		createRpu(faas)
		updateFullPinInfo(faas)
		em.create(faas)
		saveSignatory(faas)
		savePreviousFaases(faas)
		saveBackTaxes(faas)
		insertTxnReference(faas)
		return faas
	}	

	@ProxyMethod
	public void savePreviousFaases( faas ) {
		faas.previousfaases?.each{
			em.save(it, SCHEMA_PREVIOUSFAAS)
		}			
	}


	@ProxyMethod
	public def updateFaas( faas ) {
		generateUniqueTdNo(faas)
		updateNameInfo(faas)
		updateFullPinInfo(faas)
		updateRealProperty(faas)
		updateRpu(faas)
		em.update(faas)
		saveSignatory(faas)
		saveBackTaxes(faas)
		updateRpuMasterInfo(faas)
		return faas;	
	}	

	@ProxyMethod
	public void updateFaasOnly( faas ) {
		em.update(faas)
	}

	@ProxyMethod
	public def openFaasOnly( faas ) {
		return em.read(faas)
	}


	@ProxyMethod
	public def openFaas(faas){
		faas = em.findFaasById(faas)
		if (!faas) throw new Exception('FAAS does not exist or has already been deleted.')
		faas.rpu 			= rpuSvc.openRpu([objid:faas.rpuid])
		faas.rp 			= rpSvc.open([objid:faas.realpropertyid])
		faas.txntype 		= getTxnType( faas.txntype.objid )
		faas.previousfaases = em.getPreviousFaases([faasid:faas.objid])
		faas.backtaxes      = em.getBackTaxes([faasid: faas.objid]).each{ it.taxable = util.toBoolean(it.taxable, true) }
		loadSignatories(faas)
		return faas 
	}

	@ProxyMethod
	public def disapproveOnlineFaas(faas) {
		faasSvc.deleteFaas(faas)
	}

	@ProxyMethod
	public def deleteFaas( faas ) {
		if (!faas.allowdelete){
			def currentstate = getState(faas.objid)
			if (currentstate == STATE_CURRENT)  
				throw new Exception('FAAS is already current.')
			if (currentstate == STATE_CANCELLED)  
				throw new Exception('FAAS has already been cancelled.')
		}

		trackingSvc.delete(faas) 
		txnRef.delete(faas.objid)
		em.delete(faas, SCHEMA_SIGNATORY)
		em.deleteTasks(faas)
		em.deleteFaasUpdate(faas)
		em.deleteFaasStewardship(faas.rpu)

		def affectedrpus = em.getAffectedRpus(faas)
		em.deleteAffectedRpus(faas)
		affectedrpus.each{
			if (it.newfaasid){
				faasSvc.deleteFaasById(it.newfaasid)
			}
		}

		em.getPreviousFaases([faasid:faas.objid]).each{
			em.delete(it, SCHEMA_PREVIOUSFAAS)
		}

		faas.backtaxes.each{
			em.delete(it, SCHEMA_BACKTAX)
		}

		em.delete(faas)

		if (faas.txntype.newrpu){
			rpuSvc.deleteRpu(rpuSvc.openRpu(faas.rpu))
		}
		if (faas.rpu.rputype == 'land' && util.isTrue(faas.txntype.newrealproperty)){
			rpSvc.delete(rpSvc.open(faas.rp) )
		}

		if (faas.parentfaasid){
			faasSvc.deleteFaas(openFaas([objid:faas.parentfaasid]))
		}			
		
		return faas 
	}


	@ProxyMethod
	public def deletePreviousFaas( prevfaas ) {
		em.deletePreviousFaas(prevfaas)
	}

	@ProxyMethod
	public def getAffectedRpus(faas) {
		return em.getAffectedRpus(faas)
	}

	@ProxyMethod
	public void deleteFaasById( faasid ) {
		if (faasid) {
			faasSvc.deleteFaas(faasSvc.openFaas([objid:faasid]))
		}
	}	

	@ProxyMethod
	public def getTxnLogs( faasid ) {
		return em.getTxnLogs([objid:faasid])	
	}

	@ProxyMethod
	public def getTasks( faas ) {
		return em.getTasks(faas)
	}


	@ProxyMethod
	public def submitForApproval( faas ) {
		def currentstate = getState(faas.objid)
		if (currentstate == STATE_CURRENT)  
			throw new Exception('FAAS is already current.')
		if (currentstate == STATE_CANCELLED)  
			throw new Exception('FAAS has already been cancelled.')
		if (currentstate == STATE_FORAPPROVAL)  
			throw new Exception('FAAS has already been submitted for approval.')
		updateFaasState(faas, STATE_FORAPPROVAL)
		return faas
	}

	@ProxyMethod
	public void manuallyApproveFaas(faas) {
		approveFaas(faas)
	}

	@ProxyMethod
	public def approveFaas( faas ) {
		approveParentFaas(faas)
		def currentstate = getState(faas.objid)
		if (currentstate == STATE_CURRENT)  {
			return faas
		}

		if (currentstate == STATE_CANCELLED)  
			throw new Exception('FAAS has already been cancelled.')
		
		checkOpenRedFlag(faas)

		faas.state = STATE_CURRENT
		faas.prevstate = STATE_INTERIM
		updateTDNo(faas)
		updateFullPinInfo(faas)
		updateDateApprovedInfo(faas)
		em.approveFaas(faas)
		
		if (faas.txntype.newrealproperty)
			rpSvc.approve(faas.rp)

		rpuSvc.approveRpu(faas.rpu)
		updateBacktaxesInfo(faas)
		cancelPreviousFaases(faas)
		txnRef.delete(faas.objid)
		updateRpuMasterInfo(faas)
		approveAffectedRpus(faas)
		updateImprovementLandRpuIds(faas)
		return faas 
	}

	void updateBacktaxesInfo(faas){
		if (!faas.backtaxes) 
			return

		faas.backtaxes.each{
			if (!it.tdno){
				it.tdno = faas.tdno + '-' + it.ry 
				em.save(it, SCHEMA_BACKTAX)
				em.updateFaasTdNo(it)
				em.updateFaasListTdNo(it)
			}
			def pf = em.findPreviousFaas(it)
			if(pf){
				faasDb.update([objid:pf.faasid, prevtdno:it.tdno])
				faaslistDb.update([objid:pf.faasid, prevtdno:it.tdno])
			}
		}

	}

	void approveParentFaas(faas){
        if (faas.parentfaasid){
        	def pf = openFaas([objid:faas.parentfaasid])
            faasSvc.approveFaas(pf)
        }
    }

	void updateImprovementLandRpuIds(faas){
        if ('land'.equalsIgnoreCase(faas.rpu.rputype)){
        	def prevrpu = em.findPreviousLandRpu(faas)
        	if (prevrpu){
		        em.updateBldgRpuLandRpuId(prevrpu)
		        em.updateMachRpuLandRpuId(prevrpu)
		        em.updatePlantTreeRpuLandRpuId(prevrpu)
		        em.updateMiscRpuLandRpuId(prevrpu)
        	}
        }
        else if ('bldg'.equalsIgnoreCase(faas.rpu.rputype))
        	em.updateBldgRpuLandRpuIdByFaas(faas)
        else if ('mach'.equalsIgnoreCase(faas.rpu.rputype))
        	em.updateMachRpuLandRpuIdByFaas(faas)
        else if ('planttree'.equalsIgnoreCase(faas.rpu.rputype))
        	em.updatePlantTreeRpuLandRpuIdByFaas(faas)
        else if ('misc'.equalsIgnoreCase(faas.rpu.rputype))
        	em.updateMiscRpuLandRpuIdByFaas(faas)
    }


	void approveAffectedRpus(faas){
		em.getAffectedRpus(faas).each{
			def afaas = openFaas([objid:it.newfaasid])
			if(afaas){
				faasSvc.approveFaas(afaas)
			}
		}
	}

	void checkOpenRedFlag(faas){
		def openflags = em.getOpenRedFlags(faas)
		if (openflags)
			throw new Exception('Unable to approve FAAS.\nThere are still open red flags that needs to be resolved.');
	}

	@ProxyMethod
	public def approveFaasByRemote( faas ) {
		cancelPreviousFaases(faas)
		updateRpuMasterInfo(faas)
		return faas 
	}

	@ProxyMethod
	public def approveFaasByMunicipality( faas ) {
		return approveFaasByRemote(faas)
	}	


	@ProxyMethod
	public def disapproveFaas( faas ) {
		def currentstate = getState(faas.objid)
		if (currentstate == STATE_CURRENT)  
			throw new Exception('FAAS is already current.')
		if (currentstate == STATE_CANCELLED)  
			throw new Exception('FAAS has already been cancelled.')
		updateFaasState(faas, STATE_INTERIM)
		return faas 
	}	


	@ProxyMethod
	public def getState(faasid){
		def faas = em.findState([objid:faasid])
		if (!faas) 
			throw new Exception('FAAS does not exist or has already been deleted.')
		return faas.state 
	}	


	@ProxyMethod
	public def generateUniqueTdNo(faas){
		if (faas.tdno) {
			faas.utdno = faas.tdno
		}
		else {
			if (env.ORGCODE)
				faas.utdno = env.ORGCODE.replaceAll('-','') + seqSvc.getNextFormattedSeries(UTDNO_KEY)
			else 
				faas.utdno = seqSvc.getNextFormattedSeries(UTDNO_KEY)
		}
		return faas.utdno 
	}


	@ProxyMethod
	public def getTxnType( objid ){
		def txntype = getTxnTypes().find{it.objid == objid}
		if (!txntype)
			throw new Exception('Invalid Txn Type ' + objid + '.')
		return txntype
	}
	

	@ProxyMethod
    public List getTxnTypes() {
		return em.getTxnTypes([:]) 
    }	

    @ProxyMethod
    public def getTxnTypeAttributes( txntype ) {
    	return em.getTxnTypeAttributes(txntype)
    }

    @ProxyMethod
    public void saveSignatory( signatory ) {
    	if (signatory)
    		em.save(signatory, SCHEMA_SIGNATORY)
    }


    @ProxyMethod 
    public void clearSignatoryInfo(faas){
		faas.taxmapper = [:]
		faas.taxmapperchief = [:]
		faas.appraiser = [:]
		faas.appraiserchief = [:]
		faas.recommender = [:]
		faas.provrecommender = [:]
		faas.provtaxmapper = [:]
		faas.provtaxmapperchief = [:]
		faas.provappraiser = [:]
		faas.provappraiserchief = [:]
		faas.provrecommender = [:]
		faas.provapprover = [:]
		faas.approver = [:]
    }

	@ProxyMethod
	public def loadSignatories(faas){
		faas.putAll(findSignatory(faas)) 
	}

	@ProxyMethod
    public void updateSignatoryInfo(task) {
        if (task.state.matches('assign.*') || env.action?.matches('return.*'))
            return 
        if (!task.state.matches('taxmapper|taxmapper_chief|appraiser|appraiser_chief|provrecommender|recommender|approver|provtaxmapper|provtaxmapperchief|provappraiser|provappraiserchief'))
            return

        def state = task.state.replaceAll('_', '') 

        def updatefields = '';
        updatefields += state + '_taskid = $P{' + state + '_taskid},'
        updatefields += state + '_objid = $P{' + state + '_objid},'
        updatefields += state + '_name = $P{' + state + '_name},'
        updatefields += state + '_title = $P{' + state + '_title},'
        updatefields += state + '_dtsigned = $P{' + state + '_dtsigned}'

        def params = [
        	objid 					: task.refid,
        	(state + '_taskid')		: task.objid,
        	(state + '_objid')		: task.actor.objid,
        	(state + '_name')		: task.actor.name,
        	(state + '_title')		: task.actor.title,
        	(state + '_dtsigned') 	: task.enddate,
        	updatefields			: updatefields,
        ]
        em.updateSignatoryInfo(params)
    }

	@ProxyMethod
	public def findSignatory( faas ) {
		def sig = em.read(faas, SCHEMA_SIGNATORY)
		if (!sig) 
			sig = [appraiser:[:], taxmapper:[:], recommender:[:], approver:[]]
		return sig
	}

	@ProxyMethod
	public void checkDuplicateTdNo( faasid, tdno ) {
		if (tdno == null) return 
		def faas = getFaasByTdNo(tdno)	
		if (faas && faas.objid != faasid)
			throw new Exception('TD No. ' + tdno + ' already exist.')
	}	

	@ProxyMethod
	public def getFaasByTdNo(tdno){
		return em.findByTdNo([tdno:tdno])
	}	

	@ProxyMethod
	public def findFaasById(faasid) {
		return em.findFaasById([objid:faasid])
	}

	@ProxyMethod
	public def faasExist( faas ) {
		def data = em.findFaasObjid(faas)
		return data != null 
	}


	@ProxyMethod
    public def generateNextTdNo( faas ) {
    	util.required('Real Property Barangay', faas.rp.barangay)

        //TD Formatting Options:
        // Y, YY - revision year, 2 digit year 
        // YYYY - revision year, 4 digit year 
        // P - province index 
        // C - city index 
        // M - municipality index 
        // D - district index 
        // B - barangay index 
        def lguindex = em.findLguIndexInfo([barangayid:faas.rp.barangay.objid])
		def seqcount = util.toInteger2( var.td_autonumber_sequence_count, 5 )
		
        def values = [P:lguindex.provcityindex, C:lguindex.provcityindex, M:lguindex.munidistrictindex, D:lguindex.munidistrictindex, B:lguindex.barangayindex]
        def tdno   = var.td_format 
        if (!tdno) tdno = 'YY-M-B'

        if (tdno.toUpperCase().matches('.*YYYY.*')){
        	values.YYYY = faas.rpu.ry+''
        }
        if (tdno.toUpperCase().matches('.*YY.*')){
        	values.YY = (faas.rpu.ry +'')[-2..-1]
        }
        else {
        	values.Y = (faas.rpu.ry +'')[-2..-1]
        }

        values.keySet().each {
            tdno = tdno.replace( it, values[it] )
        }
        def ntdno = tdno + '-' + seqSvc.getNextFormattedSeriesA( tdno, seqcount )
        return ntdno 
    }    	


	@ProxyMethod
	public def getHistory( faasid ) {
		return em.getHistory([faasid:faasid])
	}    


	@ProxyMethod
	public def getHistoryColumns(  ) {
        return [
            [name:"ry", caption:"RY", width:50, type:'integer', format:'0000'],
            [name:"tdno", caption:"TD No.", width:140],
            [name:"prevtdno", caption:"Previous TD No.", width:140],
            [name:"txntype.objid", caption:"Txn", width:50],
            [name:"classification.code", caption:"Class", width:50], 
            [name:"fullpin", caption:"PIN", width:200],
            [name:"owner.name", caption:"Owner Name", width:250],
            [name:"cadastrallotno", caption:"Lot No.", width:100],
            [name:"totalareasqm", caption:"Area (sqm)", width:120, type:'decimal', format:'#,##0.00'],
            [name:"totalmv", caption:"Market Value", width:100, type:'decimal', format:'#,##0.00'],
            [name:"totalav", caption:"Assessed Value", width:100, type:'decimal', format:'#,##0.00'],
            [name:"cancelreason", caption:"Cancel Reason", width:100],
            [name:"canceldate", caption:"Cancel Date", width:100],
            
        ]
	}	


	@ProxyMethod
	public def getAnnotations( faasid ) {
		return em.getAnnotations([faasid:faasid])
	}    


	@ProxyMethod
	public def getAnnotationColumns(  ) {
        return [
            [name:"state", caption:"State", width:140],
            [name:"type", caption:"Type", width:140],
            [name:"txnno", caption:"Txn No.", width:60],
            [name:"txndate", caption:"Txn Date", type:'date', format:'yyyy-MM-dd', width:110], 
            [name:"fileno", caption:"File No.", width:100],
            [name:"orno", caption:"O.R. No.", width:70],
            [name:"ordate", caption:"O.R. Date", type:'date', format:'yyyy-MM-dd', width:110], 
            [name:"memoranda", caption:"Memoranda", width:250],
        ]
	}	



	@ProxyMethod
	public def formalizeOwnerInfo(entityid){
		def t = em.findIndividualEntity([objid:entityid])
		def name = '';
		if (t){
			name = t.firstname 
			if (t.middlename) 
				name += ' ' + t.middlename 
			name += ' ' + t.lastname 
		}
		return name;
	}

/*------------------------------------------------------------------------
	**
	** HELPER METHODS
	**
	------------------------------------------------------------------------*/
	def initFaas(info){
		def faas = [
			objid 		  	: util.generateId('F'),
			state 		  	: STATE_INTERIM,
			autonumber   	: util.toBoolean(var.td_autonumber, false),
			prevav		  	: '0.00',
			prevmv		  	: '0.00',
			prevareaha      : '0.000000',
			prevareasqm     : '0.00',
			prevpin     	: info.fullpin,
			effectivityyear : dtSvc.serverYear + 1,
			effectivityqtr  : 1,
			backtaxyrs		: 0,
			iscondo 		: info.iscondo,
			barangay		: info.barangay,
			datacapture     : false,
			txntype 		: info.txntype,
			lguid			: info.lgu.objid,
			lgu 			: info.lgu,
			taxpayer		: [:],
			owner 			: [:],
			administrator   : [:],
			backtaxes       : [:],
			previousfaases  : [:],
			originlguid 	: env.ORGID,
		]
		return faas 
	}


	void updateNameInfo(faas){
		def len = faas.owner?.name?.length()
		if (len > 50) len = 50
		faas.name = faas.owner?.name?.substring(0,len)
	}


	@ProxyMethod 
	public void updateFullPinInfo(faas){
		faas.fullpin = faas.rpu.fullpin 
		if (faas.rpu.rputype != 'land'){
			def landowner = em.findLandOwner([rpuid:faas.rpu.objid]);
			if (landowner && faas.taxpayer.objid != landowner.objid){
				def tokens = faas.rpu.fullpin.tokenize('-')
				tokens[4] = '(' + tokens[4] + ')'
				faas.fullpin = tokens.join('-')
			}
		}
		if (faas.rp && faas.rp.claimno ){
			if (faas.fullpin.indexOf(faas.rp.claimno) < 0)
				faas.fullpin += '-' + faas.rp.claimno
		}
	}

	@ProxyMethod 
	public void saveBackTaxes(faas){
		faas._backtaxes?.each{
			em.delete(it, SCHEMA_BACKTAX)
		}
		faas.backtaxes?.each{bt->
			bt.faasid = faas.objid 
			em.save(bt, SCHEMA_BACKTAX)
			savePreviousFaases(bt)
			updateFaasPrevInfo(bt)
		}
	}

	@ProxyMethod
	public void updateFaasPrevInfo(bt){
		def info = [:]
		info.objid = bt.objid
		info.prevtdno = (bt.prevtdno ? bt.prevtdno : 'NEW')
		info.prevpin = bt.prevpin
		info.prevowner = (bt.prevowner ? bt.prevowner : 'NEW')
		info.prevadministrator = bt.prevadministrator
		info.prevav = (bt.prevav != null ? bt.prevav : 0.00)
		info.prevmv = bt.prevmv
		info.prevareasqm = bt.prevareasqm
		info.prevareaha = bt.prevareaha
		info.preveffectivity = bt.preveffectivity
		faasDb.update(info)
	}

    void checkTxnReference(faas){
		if (faas.previousfaases && ! faas.txntype.objid.matches('SD|CS') && (faas._checkTxnRef == null || faas._checkTxnRef == true)){
			def prevfaasid = faas.previousfaases[0].prevfaasid
			txnRef.checkReference(prevfaasid)
		}
	}	


	void insertTxnReference(faas){
		if (faas.previousfaases &&  ! faas.txntype.objid.matches('SD|CS')){
			def prevfaasid = faas.previousfaases[0].prevfaasid
			def refno = ( faas.tdno == null ? 'Doc No. ' + faas.utdno : 'TD No. ' + faas.tdno  ) 
			txnRef.insert([objid:prevfaasid, refid:faas.objid, msg:'FAAS is currently referenced by ' + refno ])
		}
	}	

	@ProxyMethod
	public void updateRpuMasterInfo(faas){
		em.updateRpuMasterInfo(faas)
	}

	void updateDateApprovedInfo(faas){
		def currdate = dtSvc.serverDate
		if (!faas.approver) faas.approver = [dtsigned:currdate]
		faas.dtapproved = faas.approver.dtsigned
		if (!faas.dtapproved) {
			faas.approver.dtsigned = currdate 
			faas.dtapproved = currdate
		}
		def pdate = dtSvc.parseDate(faas.dtapproved, null)
		faas.year = pdate.year 
		faas.qtr = pdate.qtr 
		faas.month = pdate.month 
		faas.day = pdate.day 
	}

	@ProxyMethod
	public void cancelPreviousFaases(faas){
		def pdate = dtSvc.parseDate(faas.approver.dtsigned, null)
		em.getPreviousFaases([faasid:faas.objid]).each{
			def params = [
				objid  				: it.prevfaasid,
				rpuid 				: it.prevrpuid, 
				txntype 			: faas.txntype,
				cancelreason		: faas.txntype.displaycode,
				canceldate			: faas.approver.dtsigned,
				cancelledbytdnos	: faas.tdno,
			]
			cancelFaas(params)
		}

		if (faas.backtaxes){
			cancelBackTaxes(faas)
		}
	}

	void cancelBackTaxes(faas){
		for(int i=0; i<faas.backtaxes.size() - 1; i++){
			def bt = faas.backtaxes[i]
			def btf = openFaasOnly(bt)
			if (btf){
				def cf = openFaasOnly(faas.backtaxes[i+1])
				if (cf && cf.state != 'CANCELLED'){
					def params = [
						objid  				: cf.objid,
						rpuid 				: cf.rpuid, 
						txntype 			: faas.txntype,
						cancelreason		: faas.txntype.displaycode,
						canceldate			: faas.approver.dtsigned,
						cancelledbytdnos	: btf.tdno,
					]
					cancelFaas(params)
				}
			}
		}
	}

	@ProxyMethod
	public void cancelFaas(params) {
		util.required('objid', params.objid)
		util.required('rpuid', params.rpuid)
		util.required('txntype', params.txntype)
		util.required('cancelreason', params.cancelreason)
		util.required('canceldate', params.canceldate)
		util.required('cancelledbytdnos', params.cancelledbytdnos)

		def pdate = dtSvc.parseDate(params.canceldate, null)
		params.cancelledyear 	= pdate.year
		params.cancelledqtr 	= pdate.qtr
		params.cancelledmonth 	= pdate.month
		params.cancelledday 	= pdate.day 

		em.cancelFaas(params)
		faasListEm.updateFaasList(params)
		if (params.txntype.newrpu){
			em.cancelRpu([objid:params.rpuid])
		}
	}

	@ProxyMethod
	public void updateTdNoByRecord( faas ) {
		def exist = em.findFaasByTdNo(faas)
		if(exist) 
			throw new Exception('Duplicate TD No. is not allowed.\nTD No. is already assigned to PIN ' + exist.fullpin + '.')

		em.updateFaasTdNo(faas)
		em.updateFaasListTdNo(faas)
		em.updateLedgerTdNo(faas)
		em.updateLedgerFaasTdNo(faas)
	}

	@ProxyMethod
	public void updateTDNo(faas){
		def notdno = (faas.tdno == null || faas.tdno.toString().trim().length() == 0) 
		if (notdno && util.isTrue(var.faas_tdno_assigned_by_record)){
			faas.tdno = faas.utdno
		}
    	else {
    		if (notdno){
				faas.tdno = generateNextTdNo(faas)
    		}
			faas.utdno = faas.tdno 
		}
    }	
	

    def updateFaasState( faas, tostate){
		faas.state = tostate
		return em.updateFaasState([objid:faas.objid, state:tostate])
	}


	boolean allowSaveRealProperty(faas){
		def save = false;
		if (faas.rpu.rputype == 'land'){
			if (util.isTrue(faas.datacapture))
				save = true;
			else if (util.isTrue(faas.txntype.newrealproperty))
				save = true;
			else if (realpropertyDb.find([objid:faas.rp.objid]).first() == null)
				save = true;
		}
		return save;
	}

	void createRealProperty(faas){
		if (allowSaveRealProperty(faas)){
			rpSvc.save(faas.rp)
		}
	}

	void updateRealProperty(faas){
		if (allowSaveRealProperty(faas))
			rpSvc.update(faas.rp)
	}


	boolean allowSaveRpu(faas){
		def save = false;
		if (util.isTrue(faas.datacapture))
			save = true;
		else if (util.isTrue(faas.txntype.newrpu))
			save = true;
		return save;
	}

	void createRpu(faas){
		if (allowSaveRpu(faas) && rpuSvc.findRpuInfoById(faas.rpu.objid) == null){
			try{
				rpuSvc.createRpu(faas.rpu)
			}
			catch(e){
				rpuSvc.updateRpu(faas.rpu)	
			}
		}
	}

	void updateRpu(faas){
		if (allowSaveRpu(faas)){
			rpuSvc.updateRpu(faas.rpu)
		}
	}

	void validateFaasForTransaction(faas){
		def currentstate = getState(faas.objid)
		if (currentstate == STATE_CANCELLED)  
			throw new Exception('FAAS has already been cancelled.')
		if (currentstate != STATE_CURRENT)  
			throw new Exception('FAAS is not current.')

		if (faas._checkTxnRef == null || faas._checkTxnRef == true){
			txnRef.checkReference(faas.objid)
		}
	}	

	void initNewFaasInfo(info, newfaas){
		def pdate 	= dtSvc.parseCurrentDate()

		def oldid  			 	= newfaas.objid 
		def newid 			 	= util.generateId('F')
		newfaas.objid 			= newid 
		newfaas.previousfaases  = [[faasid:newid, prevfaasid:oldid]]
		newfaas.state 			= STATE_INTERIM
		newfaas.parentfaasid    = null 
		
		newfaas.prevtdno		= newfaas.tdno
		newfaas.prevpin		 	= newfaas.rpu.fullpin
		newfaas.prevowner		= newfaas.owner.name
		newfaas.prevadministrator = newfaas.administrator?.name
		newfaas.prevav			=  numSvc.format('#,##0.00', newfaas.rpu.totalav)
		newfaas.prevmv			= numSvc.format('#,##0.00', newfaas.rpu.totalmv)
		newfaas.prevareasqm		= numSvc.format('#,##0.00', newfaas.rpu.totalareasqm)
		newfaas.prevareaha		= numSvc.format('#,##0.000000', newfaas.rpu.totalareaha)
		newfaas.preveffectivity = newfaas.effectivityyear 

		newfaas.txntype 		= getTxnType( info.txntype.objid )
		newfaas.datacapture 	= info.datacapture
		newfaas.autonumber 	    = util.toBoolean(var.td_autonumber, false)
		newfaas.backtaxyrs   	= 0
		newfaas.backtaxes 		= []
		newfaas.effectivityyear = pdate.year + 1
		newfaas.effectivityqtr  = 1
		newfaas.trackingno 		= null 
		newfaas.receiver		= [type:'receiver', name:env.FULLNAME, title:env.JOBTITLE, dtsigned:dtSvc.serverDate]
		
		//clear not applicable info 
		newfaas.tdno 			= null
		newfaas.utdno 			= null
		newfaas.memoranda		= null
		newfaas.lgutype 		= (newfaas.lgutype ? newfaas.lgutype : var.lgu_type)
		newfaas.originlguid 	= env.ORGID
		clearSignatoryInfo(newfaas)
	}

	void initNewRealPropertyInfo(info, faas){
		if (faas.rpu.rputype == 'land' && faas.txntype.newrealproperty){
			def oldrpid 	= faas.rp.objid 
			def newrpid 	= util.generateId('RP')
			faas.rp.previd 	= oldrpid
			faas.rp.objid 	= newrpid 
			faas.rp.state 	= STATE_INTERIM
			faas.rp.isnew   = true
		}
		faas.realpropertyid = faas.rp.objid 
		faas.rpu.realpropertyid = faas.rp.objid
	}

	void initNewRpuInfo(info, faas){
		if (util.isTrue(faas.txntype.newrpu)){
			faas.rpu.previd = faas.rpu.objid 
			rpuSvc.generateNewIds(faas.rpu)
			faas.rpu.state 	= STATE_INTERIM
			faas.rpu.isnew  = true;
		}
		faas.rpuid = faas.rpu.objid 
	}

	@ProxyMethod
	public void insertSketch(info, faas){
		def headers = imageSvc.getImages([refid:info.faas.objid])
		headers.each{ h ->
			def imageid = h.objid
			h.objid = faas.objid 
			h.refid = faas.objid 
			imageSvc.saveHeader(h)

			imageSvc.getImageItems([objid:imageid]).each{ c ->
				c.objid = 'C' + new java.rmi.server.UID()
				c.parentid = h.objid 
				imageSvc.saveItem(c)
			}
		}
	}	

	@ProxyMethod
	public void checkLandTxnReference(rp){
		if (rp._checklandreference == null || rp._checklandreference == true){
			def land = em.findLandFaasUnderTransaction(rp)
			if (land)
				throw new Exception('Land FAAS is currently under processing and referenced by FAAS No. ' + (land.tdno ? land.tdno : land.utdno) + '.')
		}
	}

	@ProxyMethod
	public def getMaximumBackTaxYearsPolicy(  ) {
		return 10;
	}

	@ProxyMethod
	public def validateAndGetNumberBackTaxYears( effectivityyear, lastitem ) {
		def maxbaxtaxyears = getMaximumBackTaxYearsPolicy() + 1
        def lastbacktaxyear = effectivityyear - maxbaxtaxyears
        def backtaxyears = effectivityyear - lastitem.effectivityyear

        if( lastitem.effectivityyear < lastbacktaxyear ) {
            throw new Exception("Total back tax years of $backtaxyears exceeded the maximum $maxbaxtaxyears years policy.\nLast effective year must be ${lastbacktaxyear}.")
        }
        return backtaxyears
	}


    //to be intercepted by province/muni codes 
    @ProxyMethod
    public def resendToMunicipality(faas){
        return faas
    }


    @ProxyMethod
    public def getOpenRedflagCount(faasid){
    	return redFlagSvc.getOpenRedflagCount([objid:faasid])
    }


    @ProxyMethod
    public def findOpenTask( faas ) {
    	return em.findOpenTask(faas)	
    }
    
    @ProxyMethod
    public def saveAffectedRpu(arpu) {
    	em.save(arpu, 'affectedrpu')
    }

}

