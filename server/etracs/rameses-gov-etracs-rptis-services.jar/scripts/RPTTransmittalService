import com.rameses.annotations.*
import com.rameses.common.*;
import com.rameses.services.extended.*;

class RPTTransmittalService
{
	@Env 
	def env 

	@ActiveDB('rpttransmittal')
	def em 

	@Service('SequenceService')
	def seqSvc 

	@Service('DateService')
	def dtSvc 

	@Service('RPTUtil')
	def util


	def STATE_DRAFT = 'DRAFT'
	def STATE_SUBMITTED = 'SUBMITTED'
	def STATE_RECEIVED = 'RECEIVED'
	def STATE_FORAPPROVAL = 'FORAPPROVAL'
	def STATE_APPROVED = 'APPROVED'
	def STATE_DISAPPROVED = 'DISAPPROVED'

	def TYPE_FORAPPROVAL = 'FORAPPROVAL'
	def TYPE_SYNC = 'SYNC'

	def SCHEMA_ITEM = 'item'
	def SCHEMA_DATA	 = 'data'


	@ProxyMethod
	public def getList( params ) {
		if (!params.searchtext) 
			params.searchtext = '%'
		return em.getList(params)
	}

	@ProxyMethod
	public def getColumns( params ) {
		return [
			[name:'state', caption:'State', width:100],
			[name:'txnno', caption:'Txn No.', width:70],
			[name:'txndate', caption:'Txn Date', width:100, type:'date'],
			[name:'type', caption:'Type', width:120],
			[name:'tolgu.name', caption:'Transmitted To', width:300],
			[name:'createdby.name', caption:'Transmitted By', width:300],
		]
	}


	@ProxyMethod
	public void validateFaasForTransmittal( type, faas ) {
		if (type == 'SYNC') {
            if (faas.state != 'CURRENT')
                throw new Exception('FAAS state is invalid. Only CURRENT record is allowed.')
        }
        else{
        	if (util.isTrue(faas.datacapture)){
        		if (faas.state != 'FORAPPROVAL')
                	throw new Exception('FAAS is invalid. Only FORAPPROVAL state is allowed.')
        	}
        	else {
        		if (! faas.state.matches('INTERIM') )
                	throw new Exception('FAAS is invalid. Only INTERIM state is allowed.')

                //check that the last faas_task state is forprovapproval
	            def task = em.findLastFaasTask(faas)
	            if (task.state != 'forprovapproval')
	            	throw new Exception('FAAS is invalid. Only "forprovapproval" state is allowed..')
        	}
        }
	}

	@ProxyMethod
	public def create( entity ) {
		def pdate = dtSvc.parseCurrentDate();
		entity.lgu = [objid:env.ORGID, name:env.ORGNAME, type:env.ORGCLASS]
		entity.createdby = [objid:env.USERID, name:env.FULLNAME, title:env.JOBTITLE]
		entity.txnno = getNextSequence(pdate) 
		entity.txndate = pdate.date
		em.create(entity)
		return entity
	}

	@ProxyMethod
	public def update( entity ) {
		em.update(entity)
		return entity
	}

	@ProxyMethod
	public def open( entity ) {
		entity = em.read(entity)	
		if (!entity)
			throw new Exception('Transmittal does not exist or has already been deleted.')
		entity.hasimportedapproval = em.findDataCount(entity).icount > 0
		return entity
	}

	@ProxyMethod
	public void saveItem( item ) {
		checkActiveTransmittal(item)
		em.save(item, SCHEMA_ITEM)
	}

	
	@ProxyMethod
	public def getItems( entity ) {
		return em.getItems(entity)
	}

	@ProxyMethod
	public def getData( item ) {
		return em.getData(item)	
	}

	@ProxyMethod
	public void delete( entity ) {
		em.deleteAllData(entity)	
		em.deleteAllItems(entity)	
		em.delete(entity)
	}

	@ProxyMethod
	public void deleteItem( item ) {
		em.deleteItemData(item)
		em.delete(item, SCHEMA_ITEM)	
	}

	@ProxyMethod
	public void deleteData( data ) {
		em.delete(data, SCHEMA_DATA)	
	}



	@ProxyMethod
	public def submit( entity) {
		def old = open(entity)
		if (old.state != STATE_DRAFT)
			throw new Exception('Transmittal state is invalid or has been changed.\nOnly DRAFT state is allowed.')

		def items = getItems(entity)
		if (!items)
			throw new Exception('There are no items for transmittal.')

		entity.state = STATE_SUBMITTED
		update(entity)
		return entity
	}

	@ProxyMethod
	public def submitForApproval(entity){
		if (entity.type == 'SYNC')
			throw new Exception('Transmittal type is invalid. Only FORApproval Type is allowed.')
		def old = open(entity)
		if (old.state != STATE_DRAFT)
			throw new Exception('Transmittal state is invalid or has been changed.\nOnly DRAFT state is allowed.')
		entity.state = STATE_FORAPPROVAL
		update(entity)
		return entity
	}

	@ProxyMethod
	public def disapprove(entity){
		def old = open(entity)
		if (old.state != STATE_FORAPPROVAL)
			throw new Exception('Transmittal state is invalid or has been changed.\nOnly FOR APPROVAL state is allowed.')
		entity.state = STATE_DISAPPROVED
		update(entity)
		return entity
	}

	@ProxyMethod
	public def approve(entity){
		def old = open(entity)
		if (old.state != STATE_FORAPPROVAL)
			throw new Exception('Transmittal state is invalid or has been changed.\nOnly FOR APPROVAL state is allowed.')
		entity.state = STATE_APPROVED
		update(entity)
		return entity
	}



	@ProxyMethod
	public def getTransmittalTypes(  ) {
		return [TYPE_SYNC, TYPE_FORAPPROVAL]
	}



	def getNextSequence(pdate){
		def orgcode = env.ORGCODE.replaceAll('-', '')
		def key = 'RPTTRANSMITTAL-' + orgcode + '-' + pdate.year.toString() 
		return orgcode + pdate.year.toString() + seqSvc.getNextFormattedSeries(key) 
	}


	void checkActiveTransmittal(item){
		def exist = em.findActiveTransmittalItem(item)
		if (exist)
			throw new Exception('FAAS is currently included in Transmittal ' + exist.txnno + '.')
	}
}

