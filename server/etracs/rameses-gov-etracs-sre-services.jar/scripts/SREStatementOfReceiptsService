import com.rameses.annotations.*;
import com.rameses.common.*;
import com.rameses.util.*;
import java.rmi.server.*;

class SREStatementOfReceiptsService {

	@ActiveDB('sre_report')
	def em; 

	@ActiveDB('fund')
	def fund; 

	@Service('DateService')
	def dtSvc 

	@Service('NumberService')
	def numSvc

	@ProxyMethod 
	public def getFunds() { 
		def list = fund.getAll(); 
		def results = [];
		list.findAll{ it.parentid==null }?.each{a-> 
			results.add( a ); 
			list.findAll{ it.parentid==a.objid }?.each{b-> 
				results.add( b ); 
				b.title = '   ' + b.title;				
			} 
		} 
		return results; 
	} 

	@ProxyMethod 
	public def getAcctGroups() {
		return em.getAcctGroups(); 
	}

	@ProxyMethod 
	public def getReportOptions() {
		return [
			acctgroups: getAcctGroups(), 
			funds: getFunds() 
		]; 
	} 

	@ProxyMethod
	public def getReport( params ) {
		buildParams( params ); 
		params.filter = '';
		if ( params.acctgroup ) {
			params.filter += ''' AND ia.type LIKE $P{acctgroup} ''';
		}
		if ( params.fundid ) {
			params.filter += ''' 
				AND inc.fundid in ( 
					select objid from fund where objid LIKE $P{fundid} 
					union 
					select objid from fund where parentid LIKE $P{fundid} 
				) 
			'''; 
		} 

		def incomelist = null; 
		def postingtypeid = params.postingtype?.objid.toString().toUpperCase(); 
		if ( postingtypeid == 'BY_LIQUIDATION_DATE' ) {
			incomelist = em.getIncomeSummaryByLiquidationDate( params ); 
		} else {
			incomelist = em.getIncomeSummary( params ); 
		}
		
		if ( !incomelist ) 
			throw new Exception('No available income summary'); 
		if ( incomelist.find{ it.amount > 0 } == null )  
			throw new Exception('No available income summary'); 

		def incomesummarydetails = []; 
		if ( params.type == 'details' ) { 
			incomesummarydetails = ( 
				postingtypeid == 'BY_LIQUIDATION_DATE' ? 
				em.getIncomeSummaryByItemAccountsByLiquidationDate( params ) : 
				em.getIncomeSummaryByItemAccounts( params ) 
			); 
		} 
		
		incomelist.eachWithIndex{ o, idx-> o.itemindex = idx; }
		incomelist.findAll{ it.parentid=='ROOT' }.each{ 
			it.itemindex = -1;
			it.nodeindex = 1; 
			setupNode( incomelist, it ); 
		}

		def maxnodeindex = incomelist.max{ it.nodeindex }?.nodeindex; 
		if ( maxnodeindex==null ) maxnodeindex=0; 

		for (int idx=maxnodeindex; idx > 0; idx--) { 
			incomelist.findAll{ it.nodeindex==idx }.each{ o-> 
				def list = incomelist.findAll{ it.parentid==o.objid } 
				list.each{
					it.actual = ( it.amount? it.amount: 0.0 )+( it.itemamount? it.itemamount: 0.0 )
					calculateAmounts( it ); 
				}

				def target = o.target; 
				if ( target==null || target <= 0 ) { 
					o.target = list.sum{( it.target? it.target: 0.0 )} 
				} 
				o.itemamount = list.sum{( it.actual? it.actual: 0.0 )} 
				o.actual = ( o.amount? o.amount: 0.0 )+( o.itemamount? o.itemamount: 0.0 );
				calculateAmounts( o );  
			} 
		} 

		incomelist.removeAll( incomelist.findAll{( !it.actual && !it.target )});

		if ( params.type.toString().matches('standard|details') ) { 
			incomelist.removeAll( incomelist.findAll{ it.type=='subaccount' }); 
		} 

		def items = [];	
		def totaltarget = 0.0;
		def totalactual = 0.0;
		incomelist.findAll{ it.parentid=='ROOT' }.sort{ it.itemindex }.each{ o-> 
			o.title = o.account.title+'*';
			items << o; 			
			totaltarget += (o.target? o.target: 0.0); 
			totalactual += (o.actual? o.actual: 0.0); 
			setupNodeAcct( o, incomelist, incomesummarydetails, items ); 
		} 
		incomelist.clear(); 

		if ( !items ) throw new Exception('No records found'); 

		if ( params.opthidenoactual==true ) { 
			items.removeAll( items.findAll{( !it.actual )}); 
		} 

		def totals = [target: totaltarget, actual: totalactual]; 
		calculateAmounts( totals ); 

		def p = [:]; 
		p.title = 'STATEMENT OF RECEIPTS SOURCES';
		p.reportdata = items; 
		p.period = params.strperiod; 
		p.totalactual = totalactual; 
		p.totaltarget = totaltarget; 
		p.totalexcess = totals.excess; 
		p.totalpercentage = totals.percentage;
		p.totalpercentagevalue = totals.percentagevalue; 
		return p; 
	}

	private void setupNode( sourcelist, o ) {
		sourcelist.findAll{ it.parentid==o.objid }.each{ oo-> 
			oo.nodeindex = o.nodeindex+1; 
			setupNode( sourcelist, oo ); 
		} 
	}
	private def buildAcctTitle( type, data ) {
		def stype = type.toString();
		if ( stype.matches('detail|subaccount|itemaccount') ) {
    		def buff = new StringBuilder();
    		if ( stype == 'itemaccount' ) buff.append('* '); 
    		if ( data.account?.code ) buff.append( data.account?.code + ' ' );
    		if ( data.account?.title ) buff.append( data.account?.title );

    		return buff.toString(); 
    	} else { 
    		return data.account?.title; 
    	}
	} 
	private void setupNodeAcct( o, sourcelist, incomeacctlist, targetlist ) { 
    	incomeacctlist.findAll{( it.parentid==o.objid )}.each{ ia-> 
    		def stitle = buildAcctTitle( 'itemaccount', ia );
    		ia.title = "    ".multiply(o.nodeindex) + stitle; 
    		ia.actual = ia.amount; 
    		ia.type = 'itemaccount';
    		targetlist << ia; 
    	} 

		def children = sourcelist.findAll{ it.parentid==o.objid } 
		children.sort{ it.itemindex } 
		children.each{ c-> 
	    	def stitle = buildAcctTitle( c.type, c ); 
	    	c.title = (o.nodeindex > 0 ? "    ".multiply(o.nodeindex): "") + stitle;
	    	targetlist << c; 
	    	setupNodeAcct( c, sourcelist, incomeacctlist, targetlist ); 
		} 
	} 

	void calculateAmounts( c ) {
		if ( !c ) return;

		def target0 = (c.target? c.target: 0.0); 
    	def actual0 = (c.actual? c.actual: 0.0);
    	def excess0 = actual0 - target0; 
    	if ( target0 == 0.0 && actual0 == 0.0 ) {
    		c.target = c.actual = c.excess = null; 

    	} else if ( excess0 == actual0 ) {
    		c.target = null;
    		c.excess = excess0; 
    		c.percentage = 100.0;
    		c.percentagevalue = c.percentage; 
    	} else if ( target0 > 0 && actual0 == 0.0 ) {
    		c.actual = null;
    		c.excess = excess0; 
    		c.percentage = -100.0;
    		c.percentagevalue = c.percentage; 
    	} else {
    		c.excess = excess0; 
    		c.percentage = actual0 / target0; 
    		c.percentagevalue = c.percentage * 100.0; 
    	} 

    	target0 = (c.target? c.target: 0.0); 
    	actual0 = (c.actual? c.actual: 0.0);
    	c.hasamount = (target0 > 0.0 || actual0 > 0.0); 
	} 

	void buildParams( params ) {
		params.filter = '';

		def period = new StringBuffer();
		def rundate = dtSvc.getServerDate(); 
		if ( params.period == 'quarterly' ) {
			if ( !params.year ) throw new Exception('year is required');
			if ( !params.qtr ) throw new Exception('qtr is required');

			def months = dtSvc.getMonthsByQtr( params.qtr );
			def monthindex = months[0].index;
			def strmonth = (monthindex > 9? monthindex: '0'+monthindex); 
			params.startdate = params.year +'-'+ strmonth +'-01'; 
			params.enddate = dtSvc.getQtrEndDate( params.qtr, params.year ); 
			period.append( months[0].caption +' - '+ months[-1].caption +' '+ params.year ); 

		} else if ( params.period == 'asofqtr' ) {
			if ( !params.year ) throw new Exception('year is required');
			if ( !params.qtr ) throw new Exception('qtr is required');

			def months = dtSvc.getMonths(); 
			period.append( months[0].caption );
			params.startdate = params.year +'-01-01'; 
			params.enddate = dtSvc.getQtrEndDate( params.qtr, params.year ); 

			months = dtSvc.getMonthsByQtr( params.qtr );
			period.append(' - '+ months[-1].caption +' '+ params.year ); 

		} else if ( params.period == 'monthly' ) {
			if ( !params.year ) throw new Exception('year is required');
			if ( !params.month ) throw new Exception('month is required');

			def monthindex = params.month.index; 
			def strmonth = (monthindex > 9? monthindex: '0'+monthindex); 
			def dtstart = java.sql.Date.valueOf(''+ params.year +'-'+ strmonth +'-01'); 
			def dtend = dtSvc.getMonthEndDate( dtstart ); 
			def months = dtSvc.getMonths(); 
			params.startdate = dtstart;
			params.enddate = dtend;
			period.append( months[monthindex-1].caption +' '+ params.year ); 
		
		} else if ( params.period == 'asofmonth' ) {
			if ( !params.year ) throw new Exception('year is required');
			if ( !params.month ) throw new Exception('month is required');

			def monthindex = params.month.index; 
			def strmonth = (monthindex > 9? monthindex: '0'+monthindex); 
			def dtstart = java.sql.Date.valueOf(''+ params.year +'-01-01'); 
			def dtend = java.sql.Date.valueOf(''+ params.year +'-'+ strmonth +'-01');
			dtend = dtSvc.getMonthEndDate( dtend ); 

			def months = dtSvc.getMonths(); 
			params.startdate = dtstart;
			params.enddate = dtend;
			if ( months[0].index != months[monthindex-1].index ) { 
				period.append( months[0].caption ).append(' - '); 
			} 
			period.append( months[monthindex-1].caption ).append(' '+ params.year); 
		
		} else {
			if ( !params.date ) throw new Exception('date is required');

			def syear = params.date.toString().split('-')[0]; 
			if ( params.period == 'asofday' ) {
				period.append("As of "); 	
				params.startdate = syear +'-01-01'; 
			} else {
				params.startdate = params.date;
			}

			params.enddate = params.date; 
			params.year = syear; 

			def formatter = new java.text.SimpleDateFormat('MMMMM dd, yyyy');
			if ( params.date instanceof java.util.Date ) {
				period.append(formatter.format( params.date ));
			} else {
				period.append(formatter.format( java.sql.Date.valueOf( params.date ) )); 
			} 
		}
		params.strperiod = period.toString(); 
		params.fundid = params.fund?.objid; 
		params.compress = false; 

		if (!(params.enddate instanceof Date)) {
			params.enddate = java.sql.Date.valueOf( params.enddate ); 
		} 
		params.enddate = dtSvc.add( params.enddate, '1d' );
	} 
} 
