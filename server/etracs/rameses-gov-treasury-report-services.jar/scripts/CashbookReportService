import com.rameses.annotations.*;
import com.rameses.util.*;

class CashbookReportService { 

	@ActiveDB('cashbook_report')
	def report_db;

	@DataContext('sys_user_role')
	def userrole_db; 

	@DataContext('fund')
	def fund_db; 

	@DataContext('fundgroup')
	def fundgroup_db; 

	@Service('DateService')
	def dateSvc;

	@Env
	def env; 
 
	@ProxyMethod 
	public def initReport( param ) { 
		def wheresql = ['usergroup.objid = :uroleid']; 
		def whereparam = [uroleid: 'TREASURY.COLLECTOR']; 

		def tag = param.tag.toString().toLowerCase();
		if ( tag == 'report' ) { 
			// do nothing 
		} 
		else { 
			wheresql << 'objid = :collectorid'; 
			whereparam.collectorid = env.USERID; 
		} 

		def em = userrole_db.select('objid,firstname,lastname,middlename,jobtitle'); 
		def users = em.where(wheresql.join(' AND '), whereparam).list(); 
		users.unique().each { 
			it.name = [it.firstname, it.middlename, it.lastname].findAll{( it )}.join(' '); 
			it.title = it.jobtitle; 
		} 

		def funds = []; 
		fundgroup_db.where('1=1').orderBy('indexno').list().each{ fg-> 
			funds.addAll( fund_db.find([ groupid: fg.objid ]).orderBy('code').list()); 
		} 
		return [ 
			users  : users, 
			funds  : funds, 
			date   : dateSvc.getBasicServerDate(),
			year   : dateSvc.getServerYear(), 
			months : dateSvc.getMonths()
		]; 
	} 

	@ProxyMethod
	public def generateReport( params ) {
		buildDateFilter( params )
		params.accountid = params.account ? params.account.objid : env.USERID;
		params.fundid = params.fund.objid; 

		def data = [:];
		if ( params.template?.code == 'summary' ) {
			data.items = report_db.getSummaries( params ); 
		} 
		else { 
			data.items = report_db.getDetails( params ); 
		} 

		def balinfo = report_db.findBeginBalance( params ); 
		if ( balinfo?.balance ) { 
			balinfo = [refid:'begin_balance', reftype:'** BEGIN BALANCE **', dr: balinfo.balance, cr:0.0]; 
			balinfo.particulars = balinfo.reftype; 

			if ( data.items ) {
				data.items.add(0, balinfo); 
			} else {
				data.items << balinfo; 
			}
		}

		if ( !data.items ) throw new Exception("No record(s) that matches your criteria");

		def MDY = new java.text.SimpleDateFormat("MMMMM dd, yyyy");
		data.info = [
			FUNDNAME 	 : params.fund.title, 
			ACCOUNTNAME	 : (params.account ? params.account.name : env.FULLNAME),  
			ACCOUNTTITLE : (params.account ? params.account.title : env.JOBTITLE)  
		]; 
		if ( params.period?.code == 'daily' ) { 
			data.info.PERIOD = MDY.format( params.fromdate ); 
			data.info.PERIODCOVERED = data.info.PERIOD; 
		}
		else if ( params.period?.code == 'monthly' ) { 
			data.info.PERIOD = 'FOR THE MONTH OF ' + params.month.caption + ', ' + params.year; 
			data.info.FROMPERIOD = MDY.format( params.fromdate ); 
			data.info.TOPERIOD = MDY.format( dateSvc.add( params.todate, '-1d' ));  
			data.info.PERIODCOVERED = ''+ data.info.FROMPERIOD +' to '+ data.info.TOPERIOD; 
		} 
		return [reportdata: data, info: data.info]; 
	}

	void buildDateFilter( params ) { 
		def YMD = new java.text.SimpleDateFormat('yyyy-MM-dd'); 
		if ( params.period?.code == 'daily' ) { 
			if ( !params.date ) throw new Exception('date parameter is required'); 

			params.fromdate = YMD.parse( params.date.toString() ); 
			params.todate   = dateSvc.add( params.fromdate, '1d' ); 
		}
		else if ( params.period?.code == 'monthly' ) { 
			def strdate = ''+ params.year +'-'+ params.month.index +'-01';
			params.fromdate = YMD.parse( strdate );  
			params.todate   = dateSvc.add( params.fromdate, '1M' ); 
		} 
		else {
			throw new Exception("'"+ params.period?.code +"' period option not supported"); 
		}
	}

	
}
