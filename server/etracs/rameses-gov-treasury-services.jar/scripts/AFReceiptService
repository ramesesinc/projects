import com.rameses.annotations.*;
import com.rameses.util.*;
import java.rmi.server.*;
import com.rameses.services.extended.*;

class AFReceiptService  {

	@DataContext("af_control")
	def afControl;

	@DataContext("af_control_detail")
	def afControlDetail;

	@DataContext("afreceiptitem")
	def afRctItem;

	@DataContext("afreceipt")
	def afRct;

	@Env
	def env;

    def formatSeries( def o,  int len ) {
    	return  o.toString().padLeft( len, '0');
    }
	
	@ProxyMethod
	public void addNewBatch( o ) { 
		//throw new Exception("receipt id is " + o.ref.refid + " itemid is " + o.itemid );

		int len = (o.ref.item.serieslength ? o.ref.item.serieslength : 10);	
		int batchno = 1;
		def v = afControl.find( [receiptid: o.ref.refid ] ).select("batchno:{MAX(batchno)}").groupBy( "receiptid" ).first();
		if(v) {
			batchno = v.batchno + 1;
		}

		int stubno = o.startstub; 
 		int starter = (o.startseries ? Integer.parseInt( o.startseries ) : 0);
		def afid = o.ref.item.objid;
		(1..o.qty).each { 
			def m = [:];
			m.receiptid = o.ref.refid;
			m.afid = afid;
			m.txnmode = 'ONLINE';
			m.state = 'DRAFT';
			m.unit = o.ref.unit;
			if( o.ref.item.formtype == 'serial' ) {				
				m.startseries = formatSeries( starter, len  );
				m.currentseries = m.startseries;
				m.endseries = formatSeries( starter + o.ref.unitqty - 1, len );
				starter += o.ref.unitqty ;
			} else {
				m.startseries = 1;
				m.endseries = o.ref.unitqty; 
				m.currentseries = m.startseries; 
			} 
			m.dtfiled = o.ref.dtfiled;
			m.stubno = stubno++;
			m.active = 0;
			m.prefix = o.prefix;
			m.suffix = o.suffix;
			m.batchno = batchno;
			m.currentindexno = 1;
			m.respcenter = o.ref.respcenter; 
			m.cost = o.ref.cost;
			m = afControl.create(m);
		} 
		afRctItem.find( [objid: o.itemid] ).update( [qtyreceived: "{qtyreceived + :qty}"], [qty: o.qty] ); 
		def z = afRctItem.find( [objid: o.itemid] ).select( "qtyrequested,qtyreceived" ).first();
		if( z.qtyreceived > z.qtyrequested )
			throw new Exception("Qty received must be less than or equal to qty requested");
	}

	@ProxyMethod
	public def removeBatch( def o ) {
		def z = afControl.find([ receiptid: o.receiptid, batchno: o.batchno ]).select("qty:{COUNT(*)}").first();

		def zi = afRctItem.select("afid:{item.objid},unit").find( [objid: o.refid] ).first();
		afControl.find( [receiptid: o.receiptid, afid: zi.afid,  unit:zi.unit, batchno: o.batchno ] ).delete();

		//update the batchno of the afcontrols
		afControl.find( [receiptid: o.receiptid, afid: zi.afid,  unit:zi.unit] ).where("batchno > :btno", [btno: o.batchno ] ).update( [batchno: "{batchno - 1}" ] );

		afRctItem.find( [objid: o.refid] ).update( [qtyreceived: "{qtyreceived - :qty}"], [qty: z.qty] ); 
	}

	@ProxyMethod
	public def post( def o ) {
		def z = afRctItem.find( [parentid: o.objid] ).where( " (qtyrequested - qtyreceived) > 0" ).first();
		if (z) throw new Exception("Please ensure that all qty requested equal to qty received");
		def r = afRct.find([ objid: o.objid ]).select("txntype").first();

		afRct.find([ objid: o.objid ]).update([state: 'POSTED']);
		afControl.find( [receiptid: o.objid ]).update([state: 'OPEN' ]);

		if(r.txntype=='PURCHASE') {
			afControlDetail.addPurchaseReceiptDetails([ receiptid: o.objid ]); 
		}
		else {
			afControlDetail.addForwardReceiptDetails([ receiptid: o.objid ]); 
		}
		
	}

}
