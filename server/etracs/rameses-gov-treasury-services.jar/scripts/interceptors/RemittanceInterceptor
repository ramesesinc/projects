import com.rameses.annotations.*;

class RemittanceInterceptor {

	@Service("NumberService")
	def numSvc;

	@DataContext("remittance")
	def rem_db;

	@DataContext("remittance_fund")
	def remf_db;

	@DataContext("remittance_af")
	def remaf_db;

	@DataContext("cashreceipt")
	def cashreceipt_db;

	
	@After(pattern="QueryService.getNodeList", eval="#{ args[0]._schemaname == 'remittance' }")
	public void getNodes( evt ) { 
		def x = evt.args[0];
		def result = evt.result;
		result << [  title: 'Draft', state:'DRAFT' ];
		result << [  title: 'Open', state:'OPEN' ];
		result << [  title: 'Posted', state:'POSTED', liquidated: false ];
		result << [  title: 'Liquidated', state:'POSTED', liquidated:true ];
	}

	@Before(pattern="QueryService.getList", eval="#{ args[0]._schemaname == 'remittance' }")
	public void loadList(def evt) {
		def o = evt.args[0];

		if( !o.node?.state ) return;
		//o.debug = true;
		def str = "state = :state";
		def m = [state: o.node.state ]; 

		if(o.node.liquidated!=null) {
			if(o.node.liquidated==false) {
				str += " AND collectionvoucherid IS NULL ";
			}
			else {
				str += " AND NOT(collectionvoucherid IS NULL) ";
			}
		}

		if( o.where ) {
			o.where[0] = str + " AND " + o.where[0];	
			o.where[1].putAll( m );
		}
		else {
			o.where = [str, m];
		} 
	}	

	@After(pattern="PersistenceService.read", eval="#{ args[0]._schemaname == 'remittance' }" )
	public void afterRead(def evt) {
		def p = evt.result;

		if( p.amount < 1 ) { 
			def amount = Long.parseLong( p.amount.toString().split("\\.")[1] )
			p.amountinwords = numSvc.doubleToWords( amount ).toUpperCase() + " CENTAVOS";
		} else {
			p.amountinwords = numSvc.doubleToWords( p.amount ).toUpperCase() + " PESOS ONLY";
		}
	}

	@Before( pattern="PersistenceService.removeEntity", eval="#{args[0]._schemaname == 'remittance'}"  )
	public void beforeDelete( evt ) {
		def params = evt.args[0]; 
		def m = [ objid: params.objid ];
		def o = rem_db.find( m ).select('objid,state').first();
		if ( !o ) return; 

		// 
		// initiate update on state field to forcely locked the record 
		// 
		def state = o.state.toString().toUpperCase(); 
		def xstate = 'X' + state; 
		rem_db.find( m ).update([ state: xstate ]); 

		if ( !state.matches('DRAFT|OPEN')) {
			throw new Exception('This transaction is no longer in DRAFT mode'); 
		} 

		//
		// restore the original state of the record 
		// 
		rem_db.find( m ).update([ state: state ]); 

		//
		// deletion process starts here...
		// 
		m = [ remittanceid: o.objid ];
		cashreceipt_db.find( m ).update([ remittanceid: '{NULL}' ]);
		
		remf_db.find( m ).delete(); 
		remaf_db.find( m ).delete(); 
		
	} 


}
