import com.rameses.annotations.*;
import java.rmi.server.*;
import com.rameses.rules.common.*;
import com.rameses.util.*;
import com.rameses.services.extended.*;


public class IndividualCTCService
{
	@ActiveDB('ctc_individual')
	def em 

	@ActiveDB("rule")
	def ruleDB;

	@Service('DateService')
	def dtSvc

	@Service('NumberService')
	def numSvc

	@Service('RevenueItemAccountService')
	def revSvc


	@Resource("RuleService")
	def ruleSvc;

	def RULESET = 'ctcindividual'
	def FACT_PACKAGE = 'ctcindividual.'
	

	def YMD = new java.text.SimpleDateFormat('yyyy-MM-dd'); 
	def YMDHMS = new java.text.SimpleDateFormat('yyyy-MM-dd HH:mm:ss'); 

	private def convertToDate( o ) {
		if ( o instanceof Date ) {
			return o; 
		} 

		try {
			return YMDHMS.parse( o ); 
		} catch(Throwable t){
			return YMD.parse( o ); 
		} 
  	} 

	@ProxyMethod
	public def getCtcRelatedPayerData(entity){
		return [newbusiness:false];

		// info fields required:
		// newbusiness, businessgross
		// def business = em.findBusinessInfo(entity)

		/*
		def info = [
			newbusiness 	: (business.apptype == null ? false : business.apptype.equalsIgnoreCase('NEW')),
			businessgross 	: business.totalgross 
		]
		return info 
		*/
	}


	@ProxyMethod
	public def calculateTax( ctc ) { 
		if ( ctc.newbusiness == null ) ctc.newbusiness = false; 

		def data = createFacts(ctc)
		def actions = buildActions(ctc, data)
		def grps = ruleDB.getRulegroups( [ruleset: RULESET] );
		grps.each{g ->
			ruleSvc.execute( RULESET, data.facts, actions, g.name);
		}
		buildItems(data.ctc)
		return data.ctc 
	}


	@ProxyMethod
	public def getSeniorCitizenStatus(entity){
		if (entity.birthdate){
			def birthyear = dtSvc.getYear( entity.birthdate )
			def age = dtSvc.serverYear - birthyear
			return (age >= 60)
		}
		return null
	}

	void buildItems(ctc){
		ctc.items = []
		ctc.items << buildItem(ctc.acctid, ctc.amountdue)
		if (ctc.interestdue > 0.0)
			ctc.items << buildItem(ctc.interest_acctid, ctc.interestdue)
		if (ctc.brgytaxshare > 0.0)
			ctc.items << buildItem(ctc.brgytax_acctid, ctc.brgytaxshare)
		if (ctc.brgyinterestshare > 0.0)
			ctc.items << buildItem(ctc.brgyinterest_acctid, ctc.brgyinterestshare)
	}

	def buildItem(acctid, amount){ 
		return [
			objid : 'CI' + new java.rmi.server.UID(),
			item  : revSvc.open([objid:acctid]),
			amount : amount,
		]
	}


	def buildActions(ctc, data){
		def actions = new RuleAction();
		actions.addCommand('calc-basic-tax', new CalculateTax(ctc:data.ctc, numSvc:numSvc, actiontype:'calc-basic-tax'))
		actions.addCommand('calc-salary-tax', new CalculateTax(ctc:data.ctc, numSvc:numSvc,  actiontype:'calc-salary-tax'))
		actions.addCommand('calc-businessgross-tax', new CalculateTax(ctc:data.ctc, numSvc:numSvc,  actiontype:'calc-businessgross-tax'))
		actions.addCommand('calc-propertyincome-tax', new CalculateTax(ctc:data.ctc, numSvc:numSvc,  actiontype:'calc-propertyincome-tax'))
		actions.addCommand('calc-additional-tax', new CalculateTax(ctc:data.ctc, numSvc:numSvc,  actiontype:'calc-additional-tax'))
		actions.addCommand('calc-interest', new CalculateTax(ctc:data.ctc, numSvc:numSvc,  actiontype:'calc-interest'))
		actions.addCommand('calc-brgy-tax-share', new CalculateBrgyShare(ctc:data.ctc, numSvc:numSvc,  actiontype:'calc-brgy-tax-share'))
		actions.addCommand('calc-brgy-int-share', new CalculateBrgyShare(ctc:data.ctc, numSvc:numSvc,  actiontype:'calc-brgy-int-share'))
		return actions 
	}


	def createFacts(ctc)
	{
		ctc.basictax = 0.0;
		ctc.salarytax = 0.0;
		ctc.businessgrosstax = 0.0;
		ctc.propertyincometax = 0.0;
		ctc.additionaltax = 0.0;
		ctc.interest = 0.0;
		ctc.totaltax = 0.0;
		ctc.interestdue = 0.0;
		ctc.amountdue = 0.0;
		ctc.brgytaxshare = 0.0;
		ctc.brgyinterestshare = 0.0;

		def refdate = ( ctc.receiptdate ? ctc.receiptdate : dtSvc.serverDate ); 
		def pdate = convertToDate( refdate );  
		def map = [ facts:[], ctc:ctc ]; 
		
		map.facts << new CurrentDate(pdate); 
		map.facts << new EffectiveDate(pdate); 

		def brgy = ruleSvc.createFact(RULESET, FACT_PACKAGE+'Barangay'); 
		brgy.objid = ctc.barangayid; 
		map.facts << brgy; 
		
		def ctcfact = ruleSvc.createFact(RULESET, FACT_PACKAGE+'IndividualCTC'); 
		populateFactData( ctcfact, ctc );
		ctcfact.additional = ctc.hasadditional;
		map.facts << ctcfact; 
		return map; 
	}

	void populateFactData(target, sourceMap){
		sourceMap.keySet().each{
			try{
				target[it] = sourceMap[it]
			} catch(e){
				//ignore
			}
		} 
	}

}
	

public class CalculateTax implements RuleActionHandler {
	def ctc 
	def actiontype 
	def numSvc

	public void execute(def params, def drools) {
		def tax = numSvc.round(params.expr.getDecimalValue());
		if (actiontype == 'calc-basic-tax') {
			ctc.basictax = tax
			params.ctc.basictax = tax
			ctc.acctid = params.acct.key 
			ctc.accttitle = params.acct.value
		}
		else if (actiontype == 'calc-salary-tax'){
			ctc.salarytax = tax
			params.ctc.salarytax = tax
		}
		else if (actiontype == 'calc-businessgross-tax'){
			ctc.businessgrosstax = tax
			params.ctc.businessgrosstax = tax
		}
		else if (actiontype == 'calc-propertyincome-tax'){
			ctc.propertyincometax = tax
			params.ctc.propertyincometax = tax
		}
		else if (actiontype == 'calc-additional-tax'){
			ctc.additionaltax = tax
			ctc.basictax = 0.0
			ctc.salarytax = 0.0
			ctc.businessgrosstax = 0.0
			ctc.propertyincometax = 0.0
			params.ctc.additionaltax = tax
			params.ctc.basictax = 0.0
			params.ctc.salarytax = 0.0
			params.ctc.businessgrosstax = 0.0
			params.ctc.propertyincometax = 0.0
		}
		else if (actiontype == 'calc-interest'){
			ctc.interest = tax
			ctc.interestdue = tax
			params.ctc.interest = tax
			ctc.interest_acctid = params.acct.key 
			ctc.interest_accttitle = params.acct.value
		}

		ctc.totaltax = ctc.basictax + ctc.salarytax + ctc.businessgrosstax + ctc.propertyincometax + ctc.additionaltax 
		params.ctc.totaltax = ctc.totaltax 

		ctc.amountdue = ctc.totaltax
		ctc.amount = ctc.amountdue + ctc.interestdue 
		params.ctc.amountdue = ctc.amountdue
	}
}	


public class CalculateBrgyShare implements RuleActionHandler {
	def ctc 
	def actiontype 
	def numSvc

	public void execute(def params, def drools) {
		def share = numSvc.round(params.expr.getDecimalValue());

		if (actiontype == 'calc-brgy-tax-share') {
			ctc.brgytaxshare = share
			ctc.amountdue = ctc.totaltax - share 
			ctc.brgytax_acctid = params.acct.key 
			ctc.brgytax_accttitle = params.acct.value
		}
		else if (actiontype == 'calc-brgy-int-share'){
			ctc.brgyinterestshare = share
			ctc.interestdue = ctc.interest - share 
			ctc.brgyinterest_acctid = params.acct.key 
			ctc.brgyinterest_accttitle = params.acct.value
		}
	}
}	


