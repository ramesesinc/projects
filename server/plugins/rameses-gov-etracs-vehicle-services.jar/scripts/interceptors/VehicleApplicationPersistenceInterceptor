import com.rameses.annotations.*;
import java.rmi.server.UID;
import com.rameses.util.*;

/**************************************************************************************
* This interceptor is exclusively for VehicleApplicationService.create method
* This is called upon creating the application for NEW, RENEW, DROP, CHANGE_OWNER_UNIT 
***************************************************************************************/
class VehicleApplicationPersistenceInterceptor {
	
	@Env
	def env;

	@Service("WorkflowTaskService")
	def taskSvc;

	@Service("DateService")
	def dateSvc;

	@DataContext("vehicle_application")
	def appEm;

	@DataContext("vehicle_application_task")
	def appTaskEm;

	@DataContext("vehicle_application_unit")
	def appUnitEm;

	@DataContext("vehicle_application_fee")
	def appFeeEm;

	@DataContext("vehicle_franchise")
	def franchiseEm;

	@DataContext("vehicletype")
	def vehicleTypeEm;

	@Service("SequenceService")
	def seqSvc;

	@Service("ControlNoGeneratorService")
	def controlNoGenerator;

	@Service("BillingRuleService")
	def billRuleSvc;

	/*
	@Before(pattern="PersistenceService.create", index=1, eval="#{ args[0]._schemaname == 'vehicle_application' && args[0].txnmode !='CAPTURE' && args[0].apptype != 'NEW' }")
	public void checkArrearsBeforeCreateApp(def evt) {
		def entity = evt.args[0];
		def result = evt.result;
		def rootappid = entity.rootappid;
		def val = appFeeEm.where( "parent.rootappid = :rootappid ", [rootappid: rootappid] ).select("total:{SUM(amount-amtpaid)}").val();
		if( val > 0 ) throw new Exception("Cannot proceed with the transaction. There are still unpaid fees.");
	}
	*/

	@Before(pattern="PersistenceService.create", index=2, eval="#{ args[0]._schemaname == 'vehicle_application' }")
	public void beforeCreateApplicationNewOrRenew(def evt) {
		def entity = evt.args[0];

		if( entity.appyear > dateSvc.getServerYear() )
			throw new Exception("App year must be less than or equal to current year");		

		def vt = vehicleTypeEm.find( [objid: entity.vehicletypeid ] ).first();
		def dt = dateSvc.getServerDate();
		def handler = { pttn, num ->
			if( pttn == "org") {
				return env.ORGCODE;
			}
			else if( pttn == "cluster") {
				return "";
			}
			else if(pttn.startsWith("%")) {
				//this is supposed to be for the leading zeroes
				return "";
			}
			else {
				def df = new java.text.SimpleDateFormat( pttn );
				return df.format( dt );
			}
			return ret;
		}
		String prefix = controlNoGenerator.formatNumber( vt.controlpattern, handler, 0 );
		entity.appno =  prefix + seqSvc.getNextFormattedSeriesA( prefix, 6 );
		entity.amount = 0;

		//find the previous expiry date. Initially set the existing expiry date as the previous
		//we will change this only during renewal.
		if( entity.prevappid ) {
			def ma = appEm.find( [objid: entity.prevappid]).select("controlid,expirydate").first();
			entity.prevexpirydate = ma.expirydate;
			entity.expirydate = ma.expirydate;
			entity.franchise = [objid: ma.controlid]; 
		}

		//set the controlid
		entity.controlid = entity.franchise?.objid;

		//calculate the expiry date
		def p = [application: entity];
		def r = billRuleSvc.execute( [rulename:'vehicledates', params: p ] );
		entity.expirydate = r.expirydate;
	}

	@After(pattern="PersistenceService.create", index=50, eval="#{ args[0]._schemaname == 'vehicle_application' }")
	public void afterCreateApplicationNew(def evt) {
		def entity = evt.args[0];
		def result = evt.result;
		def val = (entity.apptype == "DROP") ? "{NULL}" : result.objid;
		franchiseEm.find( [objid: entity.controlid ] ).update([appid: val ]);
	}	

	@After(pattern="PersistenceService.create", index=60, eval="#{ args[0]._schemaname == 'vehicle_application' && args[0].txnmode=='ONLINE' && args[0].apptype.matches('RENEW|LATE_RENEWAL|CHANGE_OWNER_UNIT|CHANGE_UNIT|DROP')  }")
	public void afterCreateApplicationRenew(def evt) {
		def entity = evt.args[0];
		def result = evt.result;

		if(!entity.prevappid ) throw new Exception("prevappid is required in ONLINE-RENEW, CHANGE_OWNER, CHANGE_UNIT AND DROP ");

		//create a copy for each unit and replace the franchise control with the new one
		def units = appUnitEm.find( [appid: entity.prevappid ] ).list();
		units.each { z->
			z.objid = null;
			z.appid = result.objid;
			appUnitEm.create(z);
		}
	}	

	//workflow is applicable for all applications
	@After(pattern="PersistenceService.create", index=100, eval="#{ args[0]._schemaname == 'vehicle_application' }")
	public void fireApplicationWorkflow(def evt) {
		def entity = evt.args[0];
		def p = [:];
		p.processname = 'vehicle_application';
		p.refid = entity.objid;
		p.startdate = dateSvc.serverDate;
		p.info = entity;
		taskSvc.start(p);
	}

	/***************************************************************************
	* steps in removing the application
	* 1. update application taskid
	* 2. remove all tasks
	* 3. for each vehicle unit store the controlid in a list
	* 3. for each controlid in the list, update franchise vehicleunit id to null
	* 4. delete all vehicle units in the application
	* 5. for each controlid in list, update the vehicle unit id to the latest application
	* 6. remove the application   
	***************************************************************************/
	@Before(pattern="PersistenceService.removeEntity", eval="#{ args[0]._schemaname == 'vehicle_application' }")
	public void beforeApplicationRemove(def evt) {
		def entity = evt.args[0];

		def app = appEm.find( [objid: entity.objid]).first();
		if( app.task.state != "draft")
			throw new Exception("Application can only be removed from the draft state");
		def appid = app.objid;
		appEm.find( [objid:appid] ).update( [ taskid: null ] );
		appTaskEm.find( [refid: appid]).delete();

		//remove also all vehicle units for this app
		appUnitEm.find([appid: appid]).delete();


		def m = appEm.find( [controlid: app.controlid] ).where( " NOT(objid = :id) ", [id:app.objid ]).orderBy("dtcreated DESC").first();
		def val = ( m==null || m.apptype == "DROP") ? "{NULL}" : m.objid;
		franchiseEm.find( [objid: app.controlid ]).update( [appid: val] );
	}	

}