import com.rameses.annotations.*;

class WaterworksBillPrintingInterceptor{
	
	@Service("TemplateService")
	def template;

    @Service('DateService') 
    def dateSvc;    

    @DataContext('vw_waterworks_billing') 
    def bill_db;

    @DataContext('vw_waterworks_consumption') 
    def cons_db;

    @DataContext('waterworks_account_attribute') 
    def wattr_db;

	@After(pattern="WaterworksBatchBillPrintingService.formatReport")
	public void afterFormatReport( evt ) {
        def list = evt.args[0];
        def result = evt.result;

        def df = new java.text.SimpleDateFormat("yyyy-MM-dd"); 
        list.each { 
            def bill = bill_db.find([ objid: it.objid ]).first(); 
            if ( !bill ) throw new Exception('No available bill info for acct# '+ it.acctno); 

            def wattr = wattr_db.find([ parentid: it.acctid ]).where('attribute.name = :attrname', [attrname: 'SENIOR']).first();
            bill.attrname = wattr?.attribute?.name; 

            def param = [periodindexno : bill.periodindexno];
            def prevcon = cons_db.find([ acctid: it.acctid ]).select('objid,readingdate,batch.readingdate').where('periodindexno < :periodindexno', param).orderBy('periodindexno desc').first(); 
            def prevreadingdate = (prevcon?.batch?.readingdate ? prevcon?.batch?.readingdate : prevcon?.readingdate); 
            if ( prevreadingdate ) bill.prevreadingdate = prevreadingdate; 
            
            resolveData( bill ); 

            def res = template.get( "reports/waterbillreport", [ o: bill ]);
            if ( res ) result << res.toString(); 
        } 
	} 



    // 
    // helper methods 
    // 
    private def MMM_YYYY = new java.text.SimpleDateFormat("MMM yyyy"); 
    private def MMDDYYYY = new java.text.SimpleDateFormat("MM/dd/yyyy"); 
    private void resolveData( p ) {
        p.zonecode = p.zone?.code; 
        p.seqno = p.indexno.toString().padLeft(4,'0'); 
        p.blockseqno = p.zonecode.toString() +"-"+ p.seqno.toString(); 

        boolean has_discount = false; 
        def rundate = dateSvc.getBasicServerDate(); 
        if ( isDateBetween( p.readingdate, p.discdate, rundate )) {
            has_discount = true; 
        }

        p.billmonth = (p.toperiod instanceof Date ? MMM_YYYY.format(p.toperiod) : null) ;
        p.discdate = (p.discdate instanceof Date ? MMDDYYYY.format(p.discdate) : null) ;
        p.readingdate = (p.readingdate instanceof Date ? MMDDYYYY.format(p.readingdate) : null) ;
        p.prevreadingdate = (p.prevreadingdate instanceof Date ? MMDDYYYY.format(p.prevreadingdate) : null) ;
        p.duedate = (p.duedate instanceof Date ? MMDDYYYY.format(p.duedate) : null) ;

        if ( p.arrears > 0 && p.credits > 0 ) {
            def nval = p.arrears - p.credits; 
            if ( nval > 0 ) {
                p.arrears = nval; 
                p.credits = 0.0; 
            } else if ( nval < 0 ) {
                p.arrears = 0.0; 
                p.credits = nval * -1.0; 
            } else {
                p.arrears = 0.0; 
                p.credits = 0.0; 
            }
        }
        
        def totals = [ amount: p.amount, arrears: p.arrears, surcharge: p.surcharge, interest: p.interest, discount: 0.0 ];  
        totals.discount = 0.0; 
        totals.credits = p.credits; 
        totals.otherfees = p.otherfees; 
        totals.totalbill = (totals.amount + totals.otherfees + totals.arrears) - totals.credits; 
        totals.amtwithsurcharge = totals.amount + totals.surcharge; 

        if ((totals.arrears + totals.surcharge + totals.interest) > 0) {
            p.discdate = null; 
            totals.amtwithdisc = null; 
        } 
        else if ( has_discount ) {
            totals.discount = roundNearest10((totals.amount * 0.05)); 
            totals.amtwithdisc = totals.amount - totals.discount; 
        }
        else {
            p.discdate = null; 
            totals.amtwithdisc = null; 
        } 

        def amt1 = (totals.amount + totals.otherfees + totals.arrears); 
        totals.grandtotal = (amt1 + totals.surcharge + totals.interest) - totals.credits; 

        p.acctname = format(p.acctname, 46, 0); 
        p.addresstext = format(p.addresstext, 46, 0);
        p.acctno = format(p.acctno, 14, 0);
        p.blockseqno = format(p.blockseqno, 15, 2);
        p.classificationid = format(p.classificationid, 3, 0);
        p.attrname = format(p.attrname, 10, 2);
        p.readingdate = format(p.readingdate, 14, 2);
        p.reading = format(p.reading, 14, 2);
        p.prevreadingdate = format(p.prevreadingdate, 14, 2);
        p.prevreading = format(p.prevreading, 14, 2);
        p.volume = formatNumber(p.volume, 14, 2, '#,##0');
        p.amount = formatNumber(totals.amount, 14, 2, '#,##0.00');
        p.billmonth = format(p.billmonth, 14, 2);
        p.discdate = format(p.discdate, 14, 2);
        p.amtwithdisc = formatNumber(totals.amtwithdisc, 14, 2, '#,##0.00');
        p.otherfees = formatNumber(totals.otherfees, 14, 2, '#,##0.00');
        p.arrears = formatNumber(totals.arrears, 14, 2, '#,##0.00');
        p.totalbill = formatNumber(totals.totalbill, 14, 2, '#,##0.00'); 
        p.surcharge = formatNumber(totals.surcharge, 14, 2, '#,##0.00'); 
        p.credits = formatNumber(totals.credits, 14, 2, '#,##0.00'); 
        p.duedate = format(p.duedate, 14, 2); 
        p.amtwithsurcharge = formatNumber(totals.amtwithsurcharge, 14, 2, '#,##0.00'); 
        p.grandtotal = formatNumber(totals.grandtotal, 14, 2, '#,##0.00'); 

        def sb = new StringBuilder(); 
        sb.append(new String([0x1b, '@'] as char[])); 
        sb.append(new String([0x1b, 'M', 10] as char[])); 
        sb.append(new String([0x1b, '!', 8] as char[])); 
        p.initprinting = sb.toString(); 
    } 

    private String fill( int length ) { 
        StringBuilder sb = new StringBuilder(); 
        for (int i=0; i<length; i++) sb.append(" "); 
        return sb.toString(); 
    } 
    
    private String format( Object value, int length, int alignment ) {
        String str = (value == null ? "" : value.toString()); 
        int strlen = str.length(); 
        length = (length <= 0 ? strlen : length);
        if ( strlen >= length ) return str.substring(0, length);  

        int diff = length - strlen;
        diff = (diff < 0 ? 0 : diff); 
        StringBuilder sb = new StringBuilder();
        if (alignment == 2) {             
            sb.append(fill(diff)).append( str );
        } else if (alignment == 1) {
            int lpos = diff / 2; 
            lpos += ((diff % 2) > 0 ? 1 : 0); 
            sb.append(fill(lpos)).append( str ); 
            sb.append(fill(length - sb.length()));
        } else {
            sb.append( str ).append(fill(diff));
        }
        return sb.toString(); 
    }
    
    private String formatNumber( Number value, int length, int alignment, String pattern ) {
        if ( value == null ) return format("", length, alignment); 
        
        String xpattern = (pattern == null ? "" : pattern); 
        if ( xpattern.length() == 0 ) xpattern = "0"; 
        
        def numformat = new java.text.DecimalFormat( xpattern ); 
        return format( numformat.format( value ), length, alignment ); 
    }

    private def DEC_FORMAT = new java.text.DecimalFormat('0.00'); 
    private Number round( Number value ) {
        if ( value == null ) return value; 
        return new BigDecimal( DEC_FORMAT.format(value) ); 
    }
    private Number roundNearest10( Number value ) {
        if ( value == null ) return value; 

        def bd = new java.math.BigDecimal(value.toString()); 
        return bd.setScale(1, java.math.RoundingMode.HALF_UP);
    }    

    private boolean isDateBetween( startdate, enddate, rundate ) { 
        if ( startdate == null ) startdate = enddate; 
        if ( enddate == null ) enddate = startdate; 
        if (!(startdate && enddate)) return false; 
        boolean b1 = ( rundate.equals( startdate) || rundate.after( startdate));
        boolean b2 = ( rundate.equals( enddate) || rundate.before( enddate)); 
        return (b1 && b2); 
    }
}